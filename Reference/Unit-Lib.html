<html><head><title>Unit Library</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider REFERENCE (extension)</div>
<div id='categories'><a href='./../Browse.html#UnitLib'>UnitLib</a></div>
<h1>Unit Library</h1>
<div id='summary'>General description of Unit Library</div>
</div>
<div class='subheader'>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Startup'>Startup</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Udef'>Udef</a></li>
<ul class='toc'><li class='toc2'><a href='#U - Unit'>U - Unit</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#UChain'>UChain</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Arg values'>Arg values</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Smart Arguments'>Smart Arguments</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Specs'>Specs</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#UScore'>UScore</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#MassEdit'>MassEdit</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#USession'>USession</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#FreeUdef'>FreeUdef</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#MultiUdef'>MultiUdef</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Multiple servers'>Multiple servers</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#OSC control'>OSC control</a></li>
<ul class='toc'><li class='toc2'><a href='#UnitRack'>UnitRack</a></li>
<ul class='toc'></ul></ul></ul></div><h2><a class='anchor' name='Startup'>Startup</a></h2>
<pre class='code prettyprint lang-sc'>ULib.startup</pre>

<p>This will:<ul>
<li>Load all Udefs and FreeUDefs from disk.<li>Send the corresponding synthdefs.<li>Initialize global system variables.</ul>

<p> The basic element of the Library is the unit (<a href="./../Classes/U.html">U</a> ) and corresponding unit def (<a href="./../Classes/Udef.html">Udef</a>), that play a similar role to <a href="./../Classes/Synth.html">Synth</a> and <a href="./../Classes/SynthDef.html">SynthDef</a>. <h2><a class='anchor' name='Udef'>Udef</a></h2>

<p>Udef Represents a synthesis defintion. It holds:<ul>
<li>A synth graph func (from which the synthdef is generated)<li>A name.<li>Specs and Default values for each synthdef control.</ul>

<p>Declaring a Udef:<pre class='code prettyprint lang-sc'>Udef(\sine, { UOut.ar(0, SinOsc.ar(400) ) })</pre>

<p>Declaring a Udef adds it to the a global Library of Udefs, reachable at <code class='code prettyprint lang-sc'>Udef.all</code>, .e.g. <code class='code prettyprint lang-sc'>Udef.all[\sine]</code>.
<p>Udefs can be defined in plain text files containing supercollider code:<pre class='code prettyprint lang-sc'>Udef( \sine, { |freq = 440, amp = 0.1|
    UMixOut.ar( 0, SinOsc.ar( freq, 0, amp ), 0, true ) 
} ).category_( \synthesis )
    .setSpecMode( \amp, \normal )</pre>

<p>When starting up the library (ULib.startup) all the Udefs in certain folders are loaded. 
<p>You should add you own Udefs to the folder <code class='code prettyprint lang-sc'>Udef.userDefsFolder</code>.
<p>By default it is assumed that the SynthDefs are already written to files. If you want the SynthDef to be sent upon 'start' then use a LocalUdef. <h3><a class='anchor' name='U - Unit'>U - Unit</a></h3>

<p>- Represents a single synthesis process instance.<pre class='code prettyprint lang-sc'>U(\sine, [\freq, 400])</pre>

<p>Given a symbol it will search the Udefs library for a Udef with than name. <pre class='code prettyprint lang-sc'>(
x = Udef(\aNiceUdef, { 
    UOut.ar(0, SinOsc.ar(\freq.kr(400) ) ) 
});
x.loadSynthDef(s); 
)

(
y = U(\aNiceUdef, [\freq,400]); 
y.prepareAndStart(s)
)
//silence... UOut writes to private busses, you need to route the audio to the soundcard explicitely
(
y = UChain([\aNiceUdef, [\freq,400]],\stereoOutput); 
y.prepareAndStart(s);
y.gui;
)</pre>

<p>Or<pre class='code prettyprint lang-sc'>//similar to { }.play will send synthdef everytime
(
x = LocalUdef(\aNiceUdef, { 
    //nice code isn't it ?
    UOut.ar(0, SinOsc.ar(\freq.kr(400) ) ) 
});
y = U(x, [\freq,400]); 
y.prepareAndStart(s);
y.gui;
)</pre>

<p> The arguments of the unit are persistent and can be set and retrieved locally (without contacting the server), even if the unit is not playing in the server(s).<h2><a class='anchor' name='UChain'>UChain</a></h2>

<p>Units can be grouped together in chains using the UChain object. 
<p>Each chain is a rack of units, where the output of each unit can be routed to the following unit. The routing is done using private busses, and is erased after each chain, so the output of a unit of one chain can never interfere with a unit in a different chain. Units in this context can be thought of effects or plugins in a DAW, although (almost) the full flexibility of SuperCollider is available. To get input from and output to other units in the chain the UIn and UOut pseudo-ugens are used. These ugens create automatic names for controls that allow changing the private bus number. This then allows easy re-patching of the units from a gui.<pre class='code prettyprint lang-sc'>UChain(\sine, \stereoOutput).gui</pre>
<pre class='code prettyprint lang-sc'>UChain([\sine,[\freq,400]], \stereoOutput).gui</pre>
<h2><a class='anchor' name='Arg values'>Arg values</a></h2>
<ul>
<li>Arg values are persistent, they are stored in an array of key, value pairs. <li>'get' does not query the server.<li>Observers can register with a Unit to receive updates when a arg is set. <li>All guis in the Unit Lib use model-observer relationship in order to update themselves automatically.</ul>
<pre class='code prettyprint lang-sc'>x  = U(\sine);
 
y = SimpleController(x);
y.put(\freq,{ |a,b,c| postln("look at what I got: "++[a,b,c]) })

x.set(\freq, 400)</pre>

<p>Arg names can be used as method names:<pre class='code prettyprint lang-sc'>x.freq
x.freq_(500)</pre>
<h2><a class='anchor' name='Smart Arguments'>Smart Arguments</a></h2>

<p>Unit arguments are not restricted to floats, or arrays of floats, they can be instances of any class (as long as the class knows how to turn itself into an acceptable synth argument).<pre class='code prettyprint lang-sc'>x = U(\fullEQ).get(\eq);
x.dump;
x.setting</pre>
<pre class='code prettyprint lang-sc'>(
//path, startFrame = 0, endFrame, rate = 1, loop = false
x = DiskSndFile(Platform.resourceDir +/+ "sounds/a11wlk01.wav", 44100, 44100*2, 1.5, true);
y = U(\diskSoundFile,[\soundFile, x]);
UChain(y, \stereoOutput).gui
)</pre>

<p>When creating the Synths the unit will call <code class='code prettyprint lang-sc'>.asControlInputFor( server, startPos )</code> on each of the arguments. Classes that respond to <code class='code prettyprint lang-sc'>.asControlInputFor</code> properly, are valid argument values for a Unit.
<p>Examples:
<p>Extended classes:<pre class='code prettyprint lang-sc'>+ Object : asControlInputFor { |server, startPos| ^this.asControlInput }

+ Function : asControlInputFor { |server, startPos| ^this.value( server, startPos ) }

+ Rect : asControlInput { ^this.asArray }

+ Boolean : asControlInput { ^this.binaryValue }

+ Point : asControlInput { ^this.asArray }</pre>

<p>Other objects that can behave as Unit arguments:<pre class='code prettyprint lang-sc'>EQSetting : asControlInput { ^setting.flat.asControlInput }

AbstractRichBuffer: asControlInputFor { |server| 
    ^this.currentBuffer(server) 
}

BufSndFile : asControlInputFor { |server, startPos = 0| 
    ^[ this.currentBuffer(server, startPos), rate, loop.binaryValue ] 
}

DiskSndFile : asControlInputFor { |server, startPos = 0| 
    ^[ this.currentBuffer(server, startPos), rate, loop.binaryValue ] 
}</pre>
<h2><a class='anchor' name='Specs'>Specs</a></h2>

<p>Each Unit argument has an ArgSpec. <pre class='code prettyprint lang-sc'>U(\sine).def.argSpecs.dopostln</pre>

<p>The ArgSpec class has the following fields:<ul>
<li>name: name of the arg.<li>spec: subclass of Spec.<li>mode:<ul>
<li><code class='code prettyprint lang-sc'>\sync</code> - set message with sample accuracy (if possible).<li><code class='code prettyprint lang-sc'>\normal</code> - set message without time stamp, i.e., as soon as possible.<li><code class='code prettyprint lang-sc'>\init</code> - only set when the synth is created. Changing the paremeter afterwards will not send a .set message. Usually for init rate controls.<li><code class='code prettyprint lang-sc'>\nonsynth</code> - not a synth arg, not to be sent to synths. Used in MultiUdef.</ul>
</ul>

<p>Ways in which specs are defined:
<p>Explicitely:<pre class='code prettyprint lang-sc'>x = Udef(\test, { SinOsc.ar(\freq.kr(400)) },[ArgSpec(\freq, 600, \freq.asSpec)] ).argSpecs[0];
x.spec</pre>

<p>Automatically:<pre class='code prettyprint lang-sc'>Udef(\test, { SinOsc.ar(\freq.kr(400)) }).argSpecs[0].dump.spec;</pre>

<p>setSpec:<pre class='code prettyprint lang-sc'>x = Udef(\test, { SinOsc.ar(\freq.kr(400)) })
    .setSpec(\freq, \freq.asSpec)</pre>
<pre class='code prettyprint lang-sc'>U(\test).gui</pre>

<p>Current Specs:<table>
<tr><td>Classes<td>Parameters.<tr><td>ListSpec<td><tr><td>StringSpec<td><tr><td>SMPTESpec<td><tr><td>BoolSpec<td><tr><td>PointSpec<td><tr><td>PolarSpec<td><tr><td>RectSpec<td><tr><td>RangeSpec<td>minRange, maxRange;<tr><td>RichBufferSpec<td>numChannels (number), numFrames (ControlSpec) <tr><td>BufSndFileSpec<td>numChannels (can be array of numbers), numFrames (ControlSpec)<tr><td>DiskSndFileSpec<td>numChannels (can be array of numbers), numFrames (ControlSpec)<tr><td>PartConvBufferSpec<td>numChannels (number), numFrames (ControlSpec) <tr><td>MultiSpec<td>an ordered and named collection of specs, with the option to re-map to another spec<tr><td>IntegerSpec<td>default = 0, step = 1, minval = -inf, maxval = inf;<tr><td>PositiveRealSpec<td>default = 0, step = 1, minval = 0 (can only vary between 0 and +inf), maxval = inf;</table>

<p>Specs are responsible for creating the guis for the corresponding argument. <pre class='code prettyprint lang-sc'>(
x = Udef(\test,{ |bus = 0, freq = 500,thing2=1, thing3 =1,thing4,thing5, point = #[0,0]|
     Out.ar(bus,UEQ.ar( WhiteNoise.ar*0.001*point, \eq, \default )) },
    [[\bus,0,IntegerSpec()],
    [\point, Point(1,2),PointSpec(Rect(0,0,20,20))],
    [\thing2,1,BoolSpec()],
    [\thing3,1,ListSpec([1,2,3,4],2)],
    [\thing4,1,RangeSpec()] ,
    [\thing5,nil,DiskSndFileSpec()],
    [\eq, nil, UEQSpec( \default ) ]
]);
y = U(\test);
y.gui;
)</pre>
<h2><a class='anchor' name='UScore'>UScore</a></h2>

<p> Chains can be organized in time using a <a href="./../Classes/UScore.html">UScore</a>. 
<p> Each chain is also an event (inherits from <a href="./../Classes/UEvent.html">UEvent</a>) with a start time, duration and fade in and fade out times. Scores are played in real-time, and take care of preparing the events during for play playback (buffer loading, etc). The library provides a ScoreEditor GUI which works mostly as modern DAW: the start time can be changed by dragging the event on the timeline and the duration by dragging the end of the event. Many operations are available such as copy/paste, undo/redo, split, trim, duplicate, etc. Scores can have other scores as events, allowing for indefinite nesting of scores inside scores.
<p> Before playback both units, chains and scores have to prepared. This is done by calling '.prepare' on either of them (preparing a score prepares all the chains within). All the preparation is then automatically taken care of, since the "smart" arguments, such as <a href="./../Classes/BufSndFile.html">BufSndFile</a>, know how to prepare themselves.<pre class='code prettyprint lang-sc'>(
z = UScore(
    *12.collect{ |i|
        UChain( 
            i/2, //startTime
            i,     //track
            rrand(3.0,10.0), //fade times
            false, //releaseSelf
            U(\sine,[\freq, rrand(200.0,600.0)]), 
            \stereoOutput
        ).fadeOut_(1).fadeIn_(1);
    }
);
z.gui
)</pre>
<pre class='code prettyprint lang-sc'>(
z = UScore(
    *12.collect({ |i|
        var evt;
        evt = UChain(i/2,i,rrand(3.0,10.0),false,\sine, \stereoOutput).fadeOut_(1).fadeIn_(1);
        evt.units[0].set(\freq,rrand(200.0,600.0) );
        evt;
    })
);
z.gui;
)</pre>

<p>When playing a score it takes care of preparing all the resources and freeing them at the end.<pre class='code prettyprint lang-sc'>(
z = UScore(
    *12.collect({ |i|
        var evt;
        evt = UChain(
            U(\bufSoundFile, [\soundFile, BufSndFile(Platform.resourceDir +/+ "sounds/a11wlk01.wav", 
                rate: (i-6).midiratio, loop: [true,false].wrapAt(i) )
            ]),
            \stereoOutput
        ).releaseSelf_(true).startTime_(i/2).track_(i).fadeOut_(1).fadeIn_(1);
        
        if( evt.duration == inf ) {
            evt.duration = 8; // looped events stopped by UScore
        };
        evt;
    })
).gui

)</pre>

<p>UScores can be saved and loaded from files. UScore files are plain text files containing the SuperCollider compile string of the score.
<p>This capability is encoded in the 'UArchivable' class:<ul>
<li>object is saved using .asTextArchive.<li><code class='code prettyprint lang-sc'>.saveAs(path)</code> saves file, checks if file exists with that name.<li><code class='code prettyprint lang-sc'>.save</code> detects if the object has been changed since it was saved and saves it if needed to the last saved path.<li><code class='code prettyprint lang-sc'>UArchivable.read( path )</code> reconstructs the object.</ul>
<h2><a class='anchor' name='MassEdit'>MassEdit</a></h2>

<p>MassEditU and MassEditUChain allow simultaneous editing of multiple Us or UChains.
<p>MassEditU:<pre class='code prettyprint lang-sc'>(
// create 10 similar units
y = 10.collect({ U( \sine, [ \freq, 220 rrand: 880, \amp, 0.1 rrand: 0.5 ] ) });

// show them all in a window
w = Window("mass-edit", Rect(571, 101, 264, 381)).front;
w.addFlowLayout;
y.do(_.gui(w));

// create a mass editor
z = MassEditU( y );
z.gui;
)</pre>

<p>MassEditUChain:<pre class='code prettyprint lang-sc'>// create 10 similar uchains
(
y = 10.collect({ 
    UChain( 
        [ 
            [ \sine, [ \freq, 220 rrand: 880, \amp, 0.1 rrand: 0.5 ] ],
            [ \whiteNoise, [ \amp, 0.1 rrand: 0.5 ] ]
        ].choose,
        \output
    )
    .dur_( 2 rrand: 10 )
    .setGain( -10 rrand: 10 );
});


)

// show them all in a window
w = Window("mass-edit", Rect(571, 101, 360, 381), scroll: true).front;
w.addFlowLayout;
y.do(_.gui(w));

// create a mass editor
z = MassEditUChain( y ).gui;</pre>
<h2><a class='anchor' name='USession'>USession</a></h2>

<p>Allows using UChains without a timeline and starting and stopping multiple UChains/UScores/UChainGroups at the same time.
<p>Still work in progress.<pre class='code prettyprint lang-sc'>(
f = { 12.collect({ |i|
        var evt;
        evt = UChain(i/2,i+1,rrand(3.0,10.0),false,\sine, \output).fadeOut_(1).fadeIn_(1);
        evt.units[0].set(\freq,rrand(200.0,600.0) );
        evt;
    }) };
z = UScore(*(12.collect({ |i|
        var evt;
        evt = BufSndFile(Platform.resourceDir +/+ "sounds/a11wlk01-44_1.aiff", 
                rate: (i-6).midiratio, loop: [true,false].wrapAt(i) ).makeUChain
                .releaseSelf_(true).startTime_(i/2).track_(i).fadeOut_(1).fadeIn_(1);
        
        if( evt.duration == inf ) {
            evt.duration = 8; // looped events stopped by UScore
        };
        evt;
    })++f.()++[
    UScore(*f.()++[
        UScore(*f.())
    ]
)]));
z.cleanOverlaps;
x = USession(
    z,
    UChain(\sine,\output),
    UChain(\sine,\output),
    UChain(\sine,\output),
    UChain(\sine,\output),
    UChainGroup(UChain([\sine,[\freq,200]],\output),
    UChain([\sine,[\freq,400]],\output))
);
y = USessionGUI(x);
)</pre>
<h2><a class='anchor' name='FreeUdef'>FreeUdef</a></h2>

<p> For more complex uses, where a Udef does not provide the necessary functionality (e.g. multiple synths per unit per server) a <a href="./../Classes/FreeUdef.html">FreeUdef</a> class is available. By defining the 'createSynthFunc' function it's possible to have arbitrarily complex logic before instantiating the actual synth(s).<pre class='code prettyprint lang-sc'>(
d = FreeUdef( \testFree, [ \freq, 440, \amp, 0.1 ] )
    .createSynthFunc_({ |unit, target|
        Synth( "u_testFree", unit.getArgsFor( target ), target, \addToTail );
    })
    //how set values to controls
    .setSynthFunc_({ |unit ...keyValuePairs|
        unit.synths.do{ |s|
            var server = s.server;
            s.set(*keyValuePairs.clump(2).collect{ |arr| 
                [arr[0],arr[1].asControlInputFor(server)] }.flatten)
        };
    })
    .addUIO( UOut, \ar, 0, { Silent.ar } )
    .synthDef_( 
        SynthDef( "u_testFree", { |freq = 440, amp = 0.1| 
            UOut.ar( 0, SinOsc.ar( freq, 0, amp ) );
        })
    );
d.synthDef.add;
)

UChain( \testFree, \output ).gui;</pre>
<h2><a class='anchor' name='MultiUdef'>MultiUdef</a></h2>

<p>Chooses from several synthdefs using chooseFunc. Can be used for instance to create Udefs which need hardcoded number of channels.
<p>Source code of <code class='code prettyprint lang-sc'>diskSoundFile</code>:<pre class='code prettyprint lang-sc'>(
var udefs;

udefs = [1,2,3,4,5,6,7,8,10,12,16,24,32].collect({ |numChannels|
    HiddenUdef( "diskSoundFile_%".format( numChannels ).asSymbol, {
        UMixOut.ar( 0, DiskSndFilePlayer.ar( numChannels ), 0, true );
    }, [ [ \soundFile, nil, DiskSndFileSpec(nil) ] ], \soundFile )
});

//Chooses the actual SynthDef to use according to 'chooseFunc' 
//great for defs that need hardcoded channel numbers.
MultiUdef( \diskSoundFile, udefs, \soundFile )
    .chooseFunc_({ |args|
        var sf, numChannels;
        sf = (args ? []).pairsAt( \soundFile );
        if( sf.notNil ) { 
            "diskSoundFile_%".format( sf.numChannels ).asSymbol;
        } {
            \diskSoundFile_1
        };
    });

)</pre>
<h2><a class='anchor' name='Multiple servers'>Multiple servers</a></h2>

<p>It' easy to use the Unit Lib to control multiple servers at the same time. Starting a Unit will create a synth in each of the servers of the system.<pre class='code prettyprint lang-sc'>//recompile library now
(
~s1 = Server("Server1", NetAddr("localhost", 57111) ).boot.makeWindow;
~s2 = Server("Server2", NetAddr("localhost", 57112) ).boot.makeWindow; 
ULib.servers = [~s1,~s2];
ULib.startup;
)
UChain(\sine, \stereoOutput).prepareAndStart</pre>
<h2><a class='anchor' name='OSC control'>OSC control</a></h2>

<p>Most parameters of U, UChain and UScore can be controlled via OSC.
<p>Paremeters can be reached using the address pattern <strong>/scoreName/chainIndex/unitIndex/argName.subArg</strong>.<pre class='code prettyprint lang-sc'>(
var chain = UChain(
        [ 'diskSoundFile', [ 'soundFile', DiskSndFile(Platform.resourceDir +/+ "sounds/a11wlk01.wav", 44100, 44100*2, 1.5, true) ] ],
        [ 'cutFilter', [ 'freq', [ 23, 805 ] ] ],
        [ 'tremolo', [ 'speed', 5.3597421208728 ] ],
        [ 'stereoOutput']);
UScore(chain).gui;
chain.gui
)

x = UOSCsetter( UScore.current );

//just for testing
y = NetAddr( "127.0.0.1", NetAddr.langPort );

//send osc messages
y.sendMsg( '/untitled/0/0/soundFile.rate', 2);
y.sendMsg( '/untitled/0/2/speed' , 2 );

//control chain
y.sendMsg( '/untitled/0', \stop );
y.sendMsg( '/untitled/0', \play );
y.sendMsg( '/untitled/0', \release );
    
//control score
y.sendMsg( '/untitled/prepareAndStart');
y.sendMsg( '/untitled/stop');</pre>
<h3><a class='anchor' name='UnitRack'>UnitRack</a></h3>

<p>UnitRack's are just groups of units that have been properly configured or interconnected and can be inserted into other UChains.<pre class='code prettyprint lang-sc'>UnitRack(\filteredSaws,
     [U(\saw),
     U(\saw).setAudioOut(0,1),
    U(\cutFilter),
    U(\cutFilter).setAudioOut(0,1).setAudioIn(0,1),
    ]
).category_(\synth);

//open the defs window and drag the new UnitRack
UChain.new.gui</pre>

<p><div class='doclink'>source: <a href='file:///home/stefan/.local/share/SuperCollider/Extensions/Unit-Lib/HelpSource/Reference/Unit-Lib.schelp'>/home/stefan/.local/share/SuperCollider/Extensions/Unit-Lib/HelpSource/Reference/Unit-Lib.schelp</a><br>link::Reference/Unit-Lib::<br>sc version: 3.7alpha0</div></div></body></html>