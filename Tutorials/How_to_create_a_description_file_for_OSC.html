<html><head><title>How to create a description file for an OSC device</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider TUTORIALS (extension)</div>
<div id='categories'><a href='./../Browse.html#Libraries>Modality'>Libraries>Modality</a></div>
<h1>How to create a description file for an OSC device<div class='extension-indicator-ctr' title='This help file originates from a third-party quark or plugin for SuperCollider.'><img class='extension-indicator-icon' alt='Extension' src='./../images/plugin.png'><span class='extension-indicator-text'>Extension</span></div></h1>
<div id='summary'>How to create a description file to use with the Modality toolkit for an OSC device</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../Overviews/Modality.html">Modality</a>, <a href="./../Classes/MKtl.html">MKtl</a>, <a href="./../Reference/MKtl_description_files.html">MKtl description files</a>, <a href="./../Classes/OSCMon.html">OSCMon</a>, <a href="./../Tutorials/How_to_create_a_description_file.html">How to create a description file</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Introduction'>Introduction</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Simple%20input'>Simple input</a></li>
<ul class='toc'><li class='toc2'><a href='#Pattern%20matching%20on%20osc%20paths'>Pattern matching on osc paths</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Trigger%20messages'>Trigger messages</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Simple%20output'>Simple output</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Collective%20input'>Collective input</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Collective%20output'>Collective output</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Explore%20an%20OSC%20device'>Explore an OSC device</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Write%20a%20description%20file%20for%20the%20explored%20OSC%20device'>Write a description file for the explored OSC device</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#OSC%20output'>OSC output</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#OSC%20device%20info'>OSC device info</a></li>
<ul class='toc'><li class='toc2'><a href='#ipAddress'>ipAddress</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#srcPort'>srcPort</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#recvPort'>recvPort</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#recvPort'>recvPort</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#OSC%20special%20messages'>OSC special messages</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#argTemplate%20matching'>argTemplate matching</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='Introduction'>Introduction</a></h2>

<p>The functionality of <a href="./../Classes/MKtl.html">MKtl</a> relies on descriptions of the devices to be used. For some controllers, there are already descriptions available, but chances are that your device is not yet among them.
<p>This tutorial first shows simple examples of OSC interfaces, by emulating them within SuperCollider, and then describes how to create a description file for your interface.<ul>
<li><a href="#Simple%20input">Simple%20input</a><ul>
<li><a href="#Pattern%20matching%20on%20osc%20paths">Pattern%20matching%20on%20osc%20paths</a><li><a href="#Trigger%20messages">Trigger%20messages</a></ul>

<p><li><a href="#Simple%20output">Simple%20output</a><li><a href="#Collective%20input">Collective%20input</a><li><a href="#Collective%20output">Collective%20output</a><li><a href="#Explore%20an%20OSC%20device">Explore%20an%20OSC%20device</a><li><a href="#Write%20a%20description%20file%20for%20the%20explored%20OSC%20device">Write%20a%20description%20file%20for%20the%20explored%20OSC%20device</a><li><a href="#OSC%20output">OSC%20output</a><li><a href="#OSC%20device%20info">OSC%20device%20info</a><li><a href="#OSC%20Initialisation%20messages">OSC%20Initialisation%20messages</a></ul>
<h2><a class='anchor' name='Simple%20input'>Simple input</a></h2>

<p>The simplest possible OSC input would be a device sends messages with only a single value, such as this:<pre class='code prettyprint lang-sc'>// osc message path = /button, value = 1, means the button is on
/button 1
// osc message path = /button, value = 0, means the button is off
/button 0</pre>

<p>The description of this element then is:<pre class='code prettyprint lang-sc'>(
    key: 'bt',
    oscPath: '/button',
    elementType: 'button',
    ioType: \in,
    spec: \but
)</pre>

<p>An entry for <code class='code prettyprint lang-sc'>\spec</code> in the description is required to recognize this dict as an element description!
<p>Here is a complete working example of a description dict:<pre class='code prettyprint lang-sc'>(
~desc1In = (
    idInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    netAddrInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    protocol: \osc,
    elementsDesc: (
        elements: [
            (
                key: 'bt',
                oscPath: '/button',
                elementType: 'button',
                ioType: \in,
                spec: \but
            )
        ]
    )
);
)

m = MKtl( \example1in, ~desc1In );
// give the single button an action
m.elementAt(\bt).action = { |el| ["yo", el.value].postln };

// test that the action works
m.elementAt(\bt).doAction;
m.elementAt(\bt).valueAction_(1);
m.elementAt(\bt).valueAction_(0);

// create a gui for the osc device:
m.gui.parent.alwaysOnTop = true;
// post incoming OSC data
m.trace;

// and now simulate sending values from an OSC device by network:
n = NetAddr("127.0.0.1", NetAddr.langPort); // fake device address
n.sendMsg('/button', 0);
n.sendMsg('/button', 1);</pre>

<p>For a row of buttons, such devices often use the same message name, and send an index of the button to address, e.g. for two buttons:<pre class='code prettyprint lang-sc'>// osc message path = /button, index = 0, value = 1, means button at 0 is on
/button 0 1
// osc message path = /button, index = 1, value = 0, means button at 1 is off
/button 1 0</pre>

<p>The description of the elements then contains an argTemplate :<pre class='code prettyprint lang-sc'>(
key: 'bt',
elements: [
    (
    oscPath: '/button',
    argTemplate: [ 0 ],
    elementType: 'button',
    ioType: \in
    ),
    (
    oscPath: '/button',
    argTemplate: [ 1 ],
    elementType: 'button',
    ioType: \in
    )
]
)

// or shorter by defining shared properties
(
key: 'bt',
shared: (
    oscPath: '/button',
    elementType: 'button',
    ioType: \in
),
elements: [
    ( argTemplate: [ 0 ] ),
    ( argTemplate: [ 1 ] ),
]
)</pre>

<p>Here is how the argTemplate works: The incoming message is [\bt, 0, 1]; the name of the message is dropped, so the message content is [0, 1]; this message body is compared with the argTemplate [0] :<pre class='code prettyprint lang-sc'>[0]
[0, 1]</pre>

<p>-&gt; all elements contained in the argTemplate are the same as in the message body, so the message matches, and the element knows it should do its action.
<p>For a grid layout of e.g. button elements, devices usually send row and column indices of the element along, so a grid of buttons would be:<pre class='code prettyprint lang-sc'>// osc message path = /button, rowindex = 0, columnindex=1, value = 1, means the button at rowi at columni is on
/button 0 1 1
// osc message path = /button, rowindex = 1, columnindex=2, value = 0, means the button is off
/button 1 2 0</pre>

<p>Here the incoming message has 4 items: the name (which is dropped), the next two indices which are matched with the argTemplate, and the last element is the new value.
<p>The description of the elements then is:<pre class='code prettyprint lang-sc'>(
    key: 'bt',
    shared: (
        oscPath: '/button',
        argTemplate: [ 0,0 ],
        elementType: 'button',
        ioType: \in
    ),
    elements: [
        (
            elements: [
                (argTemplate: [ 0,0 ]),
                (argTemplate: [ 0,1 ])
        ]),
        (
            elements: [
                (argTemplate: [ 1,0 ]),
                (argTemplate: [ 1,1 ])
        ])
    ]
);

// With more than 2 list elements, best write more concisely with .collect.
(
key: 'bt',
shared: (
    oscPath: '/button',
    elementType: 'button',
    ioType: \in
),
elements: 2.collect { |row|
    (elements: 2.collect { |column|
        (argTemplate: [ row, column ])
    })
}
)</pre>

<p>The <code class='code prettyprint lang-sc'>argTemplate</code> can also contain <code class='code prettyprint lang-sc'>nil</code> if there is an element in the osc message that needs to be ignored while filtering, see: <a href="#argTemplate%20matching">argTemplate%20matching</a> for more examples how argTemplates can be matched.
<p>If the osc message sends some other info that we are not interested in inside the message, we can specify at which index we want to read the data (by default we take the last value in the osc message):<pre class='code prettyprint lang-sc'>// osc message path = /button, value = 1, other data = "hello", means the button is on
/button 1 "hello"
// osc message path = /button, value = 0, other data = "hello", means the button is off
/button 0 "hello"</pre>

<p>The description becomes:<pre class='code prettyprint lang-sc'>(
    key: 'bt',
    oscPath: '/button',
    elementType: 'button',
    valueAt: 1,
    ioType: \in
)</pre>

<p>After all this explanation, here is a full example of an OSC device description with one element using argTemplate, and valueAt specification. (Usually, there would be other knobs at at other row/column indices, they are left out here for shortness.)<pre class='code prettyprint lang-sc'>(
~descInput = (
    idInfo: "exampleName",
    netAddrInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    protocol: \osc,
    elementsDesc: (
        elements: [
            (
                key: 'kn',
                oscPath: '/knob',
                argTemplate: [ 1,2 ], // the knob at row 1 and column 2
                valueAt: 3, // the index in the osc message where the value is
                elementType: 'knob',
                spec: \midiCC,
                ioType: \in
            )
        ]
    )
)
)

m = MKtl( \exampleOSCInput, ~descInput );
MKtlLookup.postInfo // now also shows the opened 'osc device'

// create a gui for the osc device:
m.gui;
// post incoming data
m.trace;

n = NetAddr.new( "127.0.0.1", NetAddr.langPort );

// send messages:
n.sendMsg( '/knob', 1, 2, 63, "extra", "data" );
n.sendMsg( '/knob', 1, 2, 35, "are" );
n.sendMsg( '/knob', 1, 2, 34, "ignored" );

// free the device again
m.free;</pre>
<h3><a class='anchor' name='Pattern%20matching%20on%20osc%20paths'>Pattern matching on osc paths</a></h3>

<p>Some devices may send a different on and off message, e.g. <code class='code prettyprint lang-sc'>"/button/on"</code> for the on state, and <code class='code prettyprint lang-sc'>"/button/off"</code> for the off state.
<p>So the oscPath you want to match is <code class='code prettyprint lang-sc'>"/button/*"</code> - the <code class='code prettyprint lang-sc'>*</code> is the standin for the possible matches you want to look for. In an <a href="./../Classes/ItemsSpec.html">ItemsSpec</a> you define the possible states that can be sent, in the order that they may appear, in our simple example here: <code class='code prettyprint lang-sc'>["off","on"]</code> which means that <code class='code prettyprint lang-sc'>"off"</code> will map to 0 and <code class='code prettyprint lang-sc'>"on"</code> will map to 1. If you added a third item <code class='code prettyprint lang-sc'>["off","maybe","on"]</code>, the middle item would map to 0.5.<pre class='code prettyprint lang-sc'>(
~descInput = (
    idInfo: "example",
    netAddrInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    specs: (
        onOffString: ItemsSpec.new( [ "off", "on" ] )
    ),
    protocol: \osc,
    elementsDesc: (
        elements: [
            (
                key: 'bt',
                oscPath: '/button/*',
                elementType: 'button',
                spec: \onOffString,
                ioType: \in
            )
        ]
    )
)
);

m = MKtl( \exampleOSCInput, ~descInput );
MKtlLookup.postInfo // now also shows the opened 'osc device'

// create a gui for the osc device:
m.gui;
// post incoming data
m.trace;

n = NetAddr.new( "127.0.0.1", NetAddr.langPort );

// send messages:
n.sendMsg( "/button/off" );
n.sendMsg( "/button/on" );


// free the device again
m.free;</pre>
<h3><a class='anchor' name='Trigger%20messages'>Trigger messages</a></h3>

<p>In some cases, an OSC message has no relevant arguments and just functions as a trigger that a certain event happened. In this case you use the type <code class='code prettyprint lang-sc'>\trigger</code> for the element. The output value of the element will always be 1.<pre class='code prettyprint lang-sc'>(
~descInput = (
    idInfo: "example",
    // netAddrInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    protocol: \osc,
    elementsDesc: (
        elements: [
            (
                key: 'tr',
                oscPath: '/trigger',
                elementType: 'trigger',
                ioType: \in,
                spec: \unipolar  // spec is needed, but its value is ignored
            )
        ]
    )
)
);

m = MKtl( \exampleOSCInput, ~descInput );
MKtlLookup.postInfo  // now also shows the opened 'osc device'

// create a gui for the osc device:
m.gui;
// post incoming data
m.trace;

m.elAt(\tr).action = { "TRIG!".postln };
m.elementAt(\tr).doAction;

n = NetAddr.new( "127.0.0.1", NetAddr.langPort );

// send messages:
n.sendMsg( "/trigger" );
n.sendMsg( "/trigger" );

// free the device again
m.free;</pre>
<h2><a class='anchor' name='Simple%20output'>Simple output</a></h2>

<p>A simple OSC output is an OSC interface that receives OSC messages with only a value for a single control on the device. For example:<pre class='code prettyprint lang-sc'>// turn on the led at brightness value 5
/led 5
// turn off the led meaning brightness value 0
/led 0</pre>

<p>With a description like:<pre class='code prettyprint lang-sc'>(
    key: 'led',
    oscPath: '/output',
    elementType: 'led',
    ioType: \out
);
)</pre>

<p>Just like with the simple input example, we can have indices that define additional arguments that specify which led should be on.<pre class='code prettyprint lang-sc'>// turn on the led at row 2 and column 3 at brightness value 5
/led 2 3 5
// turn off the led at row 2 and column 3 meaning brightness value 0
/led 2 3 0</pre>

<p>With a description like:<pre class='code prettyprint lang-sc'>(
    key: 'led',
    oscPath: '/output',
    argTemplate: [ 2, 3 ],
    elementType: 'led',
    ioType: \out
);</pre>

<p>If the value needs to be inserted in between identifiers for the message, you can use <code class='code prettyprint lang-sc'>nil</code> as a placeholder in the <code class='code prettyprint lang-sc'>argTemplate</code><pre class='code prettyprint lang-sc'>// turn on the led at row 2 and column 3 at brightness value 5
/led 2 5 3
// turn off the led at row 2 and column 3 meaning brightness value 0
/led 2 0 3</pre>

<p>With a description like:<pre class='code prettyprint lang-sc'>(
(
    key: 'led',
    oscPath: '/output',
    argTemplate: [ 2, nil, 3 ],
    elementType: 'led',
    ioType: \out
);
)</pre>

<p>An example of opening an OSC device from a description and sending output to it:<pre class='code prettyprint lang-sc'>(
MKtl.addSpec(\pwm8bit, [0, 255, \linear, 1]);

~descOutput = (
    // only for testing: send output values to SC itself
    // (instead of a real osc device)
    idInfo: "test",
    netAddrInfo: ( ipAddress: "127.0.0.1", srcPort: NetAddr.langPort, recvPort: NetAddr.langPort ),
    protocol: \osc,
    elementsDesc: (
        elements: [
            (
                key: 'led',
                oscPath: '/output',
                // message will be: "/output" 2 3 &lt;val&gt;
                argTemplate: [ 2, 3 ],
                'type': 'led', spec: \pwm8bit,
                \ioType: \out
            )
        ]
    )
);
);
m = MKtl( \exampleOSCoutput, ~descOutput );
// an output element in an OSC MKtl sends its value out by network
// when the value of the element in the MKtl is changed.
// to simulate the receiving osc device, we listen in SC itself

// listen to all incoming osc, so we see the value setting message:
OSCFunc.trace( true );
// set a value:
m.elAt( \led ).value_( 0.6 );
m.elAt( \led ).value_( 1.0.rand );
// set and send in original value range of the device:
m.elAt( \led ).deviceValue_( 256.rand.postln );

// cleanup:
m.free;</pre>
<h2><a class='anchor' name='Collective%20input'>Collective input</a></h2>

<p>Some OSC devices will send the data for several elements within one OSC message. For example an OSC message updating all 5 buttons of a device:<pre class='code prettyprint lang-sc'>/buttons 1 1 0 1 0</pre>

<p>In this case, we can create a <code class='code prettyprint lang-sc'>collective</code> that will listen for the <code class='code prettyprint lang-sc'>/buttons</code> message, and pass it on to the elements that are part of the collective:<pre class='code prettyprint lang-sc'>(
~descCollectiveInput = (
    idInfo: ( ipAddress: "127.0.0.1" ),
    protocol: \osc,
    collectives: (
        buttons: (
            oscPath: 'buttons',
            elements: [ [\bt,0], [\bt,1], [\bt,2], [\bt,3], [\bt,4] ], // the order in this array determines the order how the message is parsed
            ioType: \in
        )
    ),
    elementsDesc: (
        elements: [
            (
                key: 'bt',
                shared: ( elementType: 'button', ioType: \collectiveIn, \spec: \but ),
                elements: 5.collect { () }
            )
        ]
    )
);
)
m = MKtl( \exampleCollectiveIn, ~descCollectiveInput );
m.gui;
m.trace;

// test setting it directly
m.collectivesDict.buttons.deviceValueAction_([ 1, 1, 0, 0, 1 ].scramble);

// and now from network:
n = NetAddr.new( "127.0.0.1", NetAddr.langPort );
n.sendMsg( "/buttons", *[ 1, 1, 0, 0, 1 ].scramble );

// send new random states
(
Tdef( \autoaction, {
    loop {
        n.sendMsg( "/buttons", 2.rand, 2.rand, 2.rand, 2.rand, 2.rand );
        1.0.wait;
    };
}).play;
);

m.free;</pre>

<p>As in the simple input examples, we can use an <code class='code prettyprint lang-sc'>argTemplate</code> for collectives messages. Any wildcards (<code class='code prettyprint lang-sc'>nil</code>) in the template will be assumed to contain relevant data. If you want a field to be ignored in the template, and it does not contain relevant data, you can use <code class='code prettyprint lang-sc'>valueAt</code> to specify which indices in the OSC message contain the values that are your desired input data. See the following examples for clarification:
<p>Matching a simple argument template with the message ID is at the beginning of the message body:<pre class='code prettyprint lang-sc'>(
~descCollectiveInput = (
    idInfo: ( ipAddress: "127.0.0.1" ),
    protocol: \osc,
    collectives: (
        buttons: (
            // would fire then do something with the elements inside
            oscPath: '/buttons',
            argTemplate: [ 7 ], // any message beginning with [7] is accepted
            // the order in this array determines the order how the message is parsed:
            elements: [ [\bt,0], [\bt,1], [\bt,2], [\bt,3], [\bt,4] ],
            ioType: \in
        )
    ),
    // and the five button elements
    elementsDesc: (
        elements: [
            (
                key: 'bt',
                shared: ( elementType: 'button', ioType: \collectiveIn, \spec: \but ),
                elements: 5.collect { () }
            )
        ]
    )
);
)

m = MKtl( \exampleCollectiveIn, ~descCollectiveInput );
m.gui;
m.trace;

n = NetAddr.new( "127.0.0.1", NetAddr.langPort );
// means messages like this are received, because message body starts with 7:
n.sendMsg( "/buttons", 7, *[0, 1, 1, 0, 1].scramble );

// while this message is ignored: (4 != 7)
n.sendMsg( "/buttons", 4, 0, 1, 1, 0, 1 );</pre>

<p>Matching an argument template with the ID to filter by at the end:<pre class='code prettyprint lang-sc'>(
~descCollectiveInput = (
    idInfo: ( ipAddress: "127.0.0.1" ),
    protocol: \osc,

    collectives: (
        data: (
            // would fire the do something with the elements inside
            oscPath: '/buttons',
            argTemplate: [ nil, nil, nil, nil, nil, 7 ],
            elements: [ [\bt,0], [\bt,1], [\bt,2], [\bt,3], [\bt,4] ], // the order in this array determines the order how the message is parsed
            ioType: \in
        )
    ),
    elementsDesc: (
        elements: [
            (
                key: 'bt',
                shared: ( elementType: 'button', ioType: \collectiveIn, \spec: \but ),
                elements: 5.collect { () }
            )
        ]
    )
);
)

m.rebuild( ~descCollectiveInput );
m.trace;
m.gui;

// now messages _ending_ with 7 are accepted:
n.sendMsg( "/buttons", 2.rand, 2.rand, 2.rand, 2.rand, 2.rand, 7 );

// and messages with other final items are ignored:
n.sendMsg( "/buttons", 0, 1, 1, 0, 1, 4 );</pre>

<p>Here is an example for matching an argument template with a <code class='code prettyprint lang-sc'>nil</code> wildcard at the beginning, but interesting values are only later on in the message, so we use <code class='code prettyprint lang-sc'>valueAt</code> explicitly:<pre class='code prettyprint lang-sc'>(
~descCollectiveInput = (
    idInfo: ( ipAddress: "127.0.0.1" ),
    protocol: \osc,
    collectives: (
        data: (
            // would fire the do something with the elements inside
            oscPath: '/buttons',
            argTemplate: [ nil, 7 ],
            valueAt: [ 3, 4, 5, 6, 7 ],
            elements: [ [\bt,0], [\bt,1], [\bt,2], [\bt,3], [\bt,4] ], // the order in this array determines the order how the message is parsed
            ioType: \in
        )
    ),
    elementsDesc: (
        elements: [
            (
                key: 'bt',
                shared: ( elementType: 'button', ioType: \collectiveIn, \spec: \but ),
                elements: 5.collect { () }
            )
        ]
    )
);
)

m.rebuild( ~descCollectiveInput );
m.trace;
m.gui;

// messages matching [nil, 7] are received:
n.sendMsg( "/buttons", "blah".scramble, 7, 0, 1, 1, 0, 1 );
n.sendMsg( "/buttons", "blah".scramble, 7, 1, 1, 1, 1, 1 );

// others are ignored
n.sendMsg( "/buttons", "blah".scramble, 4, 0, 1, 1, 0, 1 );


m.free;</pre>
<h2><a class='anchor' name='Collective%20output'>Collective output</a></h2>

<p>Some OSC devices will listen for data of several elements within a single OSC message. For example an OSC device may have 3 LEDs which are set with one message for the array of 3 LEDs:<pre class='code prettyprint lang-sc'>/leds 1 1 0</pre>

<p>In this case, we can create a <code class='code prettyprint lang-sc'>collective</code> that will send the <code class='code prettyprint lang-sc'>/leds</code> message, whenever the value of one of the elements of the collective changes:<pre class='code prettyprint lang-sc'>(
MKtl.addSpec(\pwm8bit, [0, 255, \linear, 1]);

~descCollectiveOut = (
    idInfo: ( ipAddress: "127.0.0.1", recvPort: NetAddr.langPort ),
    protocol: \osc,
    'collectives': (
        leds: (
            oscPath: '/leds',
            // elements that make up the group in the order in which
            // they should appear in the output message:
            elements: [ [\led,0], [\led, 1], [\led, 2] ],
            argTemplate: [ 7 ], // default arguments to send
            ioType: \out
        )
    ),
    elementsDesc: (
        elements: [
            (
                key: 'led',
                shared: (
                    default: 0,
                    elementType: 'led', spec: \pwm8bit,
                    // use a group method to create the output
                    ioType: \collectiveOut
                ),
                elements: 3.collect { () }
            )
        ]
    )
);
)

m = MKtl( \exampleCollectiveOut, ~descCollectiveOut );
m.gui;

// test to see what OSC messages are coming in -
// fake device is within SC, so we see messages to it by trace:
OSCFunc.trace( true );

// the collective sends an array of 3 led values via OSC
// when setting value of a single element
m.elAt( \led, 1 ).value_( 0.5 );
m.elAt( \led, 2 ).value_( 0.8 );
m.elAt( \led, 0 ).value_( 0.1 );

// if you set the value(s) of the group, the send happens 3 times:
m.elAt(\led).value_([1, 2, 3] / 3);
m.elAt(\led).value;
m.collectivesDict[\leds].value_([0, 1, 2] / 2);
m.elementAt(\led).value;


// instead, you can do a collective send directly:
m.send(\leds, [0,64,255]);


(
Tdef( \autoaction, {
    loop {
        m.elementAt( \led, 0 ).value_( 1.0.rand );
        1.0.wait;

        m.elementAt( \led, 1 ).value_( 1.0.rand );
        1.0.wait;

        m.elementAt( \led, 2 ).value_( 1.0.rand );
        1.0.wait;
    };
}).play;
);

Tdef( \autoaction ).stop;</pre>

<p>If the value needs to be inserted in between identifiers for the message, you can use <code class='code prettyprint lang-sc'>nil</code> as the place holder in the <code class='code prettyprint lang-sc'>argTemplate</code><pre class='code prettyprint lang-sc'>(
MKtl.addSpec(\pwm8bit, [0, 255, \linear, 1]);

~descCollectiveOut = (
    idInfo: ( ipAddress: "127.0.0.1", recvPort: NetAddr.langPort ),
    protocol: \osc,
    'collectives': (
        leds: (
            oscPath: '/leds',
            elements: [ [\led,0], [\led, 1], [\led, 2] ], // elements that make up the group in order that they should appear in the output message
            argTemplate: [ nil, 7, nil, 9, 10 ], // default arguments to send elements take up position 0, 2 and 5 and so on.
            ioType: \out
        )
    ),
    elementsDesc: (
        elements: [
            (
                key: 'led',
                shared: (
                    default: 0,
                    elementType: 'led', spec: \pwm8bit,
                    // use a group method to create the output
                    ioType: \collectiveOut
                ),
                elements: 3.collect { () }
            )
        ]
    )
);
)
m.free;
m = MKtl( \exampleCollectiveOut, ~descCollectiveOut );
m.trace;
m.gui;
OSCFunc.trace;
Tdef( \autoaction ).play;

m.free;</pre>
<h2><a class='anchor' name='Explore%20an%20OSC%20device'>Explore an OSC device</a></h2>

<p>To find out what an OSC device can do, the first thing you can do is read the documentation for the device and see what it tells about the OSC interface. Usually the documentation lists a number of messages that the device sends out, and a number of messages that it reacts upon. For OSC output to the device you have to rely on the documentation about the device to find out what you need to send to it. For the OSC input from the device, there is a tool (<a href="./../Classes/OSCMon.html">OSCMon</a>) to find out what messages it sends.<pre class='code prettyprint lang-sc'>// create a fake OSC device that sends some messages:
(
n = NetAddr.new( "127.0.0.1", NetAddr.langPort );
Tdef( \fakeOSCdevice, {
        // a 3 by 3 grid of pads that send out 7 bit data each
    loop {
        n.sendMsg( "/pad", 3.rand, 3.rand, 128.rand );
        1.0.rand.wait;
    };
});

Tdef( \fakeOSCdevice ).play;
);

// start an OSCMon:

o = OSCMon.new;
o.enable; // enable it
o.show; // show a gui to view the data
o.verbose = true; // post the messages

// find out from which addresses data was sent:
o.addresses;

// messages names that were sent:
o.msgNames;
o.verbose = false;

// In OSCMon, tracked sources can have nicknames
// - some are already here by default:
o.anaDict.nicknames;
// you can ask which message names come from which addresses:
o.anaDict.msgNamesByAddr;
// and you can ask for sample messages from each address
o.anaDict.messagesByAddr;
o.anaDict.messagesByAddr.at(n).printAll;"";</pre>

<p>In this code example, we find out that there is a <code class='code prettyprint lang-sc'>NetAddr( "127.0.0.1", 57120 )</code> sending an OSC message like this: <code class='code prettyprint lang-sc'>[ '/pad', 0, 2, 67 ]</code>.
<p>For your actual OSC device you would move each control and look at what the incoming message looks like. This way, you can figure out which controls send out which OSC message with which parameters.
<p>For our (fake) example device, we find out that there is only one type of message sent out (<code class='code prettyprint lang-sc'>'/pad'</code>), with the first two arguments being a row and column index, and the last value being a kind of velocity value.<h2><a class='anchor' name='Write%20a%20description%20file%20for%20the%20explored%20OSC%20device'>Write a description file for the explored OSC device</a></h2>

<p>Based on our exploration, we can start writing the description file, we first make a simple example for one pad:<pre class='code prettyprint lang-sc'>(
~descExample = (
    idInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    protocol: \osc,
    // name: \descEx,
    elementsDesc: (
        elements: [
            (
                key: 'pad',

                oscPath: '/pad',
                argTemplate: [ 0,0 ], // at row 0 and column 0
                elementType: 'pad',
                spec: \midiCC,
                ioType: \in
            )
        ]
    )
);
)

m = MKtl( \exampleOSCDesc, ~descExample );
m.device;
MKtl.all;

MKtlLookup.postInfo  // now also shows the opened 'osc device'

// create a gui for the new osc device:
m.gui;

// post incoming data
m.trace;

m.free;</pre>

<p>So if this seems to work, we can expand the description to have a grid of 3 x 3 pads:<pre class='code prettyprint lang-sc'>(
~descExample = (
    idInfo: ( srcPort: NetAddr.langPort, ipAddress: "127.0.0.1" ),
    protocol: \osc,
    elementsDesc: (
        shared: (
                    oscPath: '/pad',
                    elementType: 'pad',
                    spec: \midiCC,
                    ioType: \in
                ),
        elements: [
            (
                key: 'pad',
                elements: 3.collect { |id|
                    (elements: 3.collect { |jd|
                        (argTemplate: [ id,jd ])
                    })
            })
        ]
    )
);
)

m.rebuild( ~descExample );
m.gui
m.elementsDict

// test with a global action
m.elementGroup.action = { |el| ("  recv:" + [m, el.name, el.value.round(0.001)]).postln; };
m.elementAt(\pad, 0, 2).deviceValueAction_(128.rand.postln);

// and test that m receives properly via OSC:
(
n = NetAddr.localAddr;
Tdef(\exosc, {
    inf.do {
        "send: ".post;
        n.sendMsg(*['/pad', 3.rand, 3.rand, 128.rand].postcs);
        0.25.wait;
    }
}).play;
)
Tdef(\exosc).stop;
Tdef(\exosc).play;</pre>
<h2><a class='anchor' name='OSC%20output'>OSC output</a></h2>

<p>As an example, for the libmanta we find the description of (some of) the OSC output messages for the MantaOSC application:<pre class='code prettyprint lang-sc'>For commands that take color strings, the value can be either "red", "amber", or "off".

"/manta/led/pad"
Takes a color string and an integer representing the pad LED to be set.

"/manta/led/button"
Takes a color string and an integer representing the pad LED to be set.</pre>

<p>From this we understand that we should make two types of output elements. For the button leds we need:<pre class='code prettyprint lang-sc'>(
key: \bt,
elements: 4.collect { |id|
    (
        key: \led,
        oscPath: '/manta/led/button',
        argTemplate: [ nil, id ],
        elementType: 'led',
        spec: 'mantaLed',
        ioType: \out
    );
};
);</pre>

<p>For the pad leds we need:<pre class='code prettyprint lang-sc'>(
key: \pad,
elements: 48.collect { |id|
    (
        key: \led,
        oscPath: '/manta/led/pad',
        argTemplate: [ nil, id ],
        elementType: 'led',
        spec: 'mantaLed',
        ioType: \out
    );
};
);</pre>

<p>The <code class='code prettyprint lang-sc'>spec</code> needed is a special one, a <a href="./../Classes/ItemsSpec.html">ItemsSpec</a> that defines the mapping of a value to one of the three colors, we can define it in the <code class='code prettyprint lang-sc'>specs</code> part of the device description:<pre class='code prettyprint lang-sc'>specs: (
    // led can be off, amber or red
    mantaLed: ItemsSpec( ["off","amber","red"] ),
)</pre>

<p>For the complete Manta description take a look at its description file:<pre class='code prettyprint lang-sc'>MKtlDesc('snyderphonics-manta').openFile;</pre>
<h2><a class='anchor' name='OSC%20device%20info'>OSC device info</a></h2>

<p>The underlying protocol of OSC communication in SuperCollider and most other applications is UDP. The addressing of messages is done via IP adresses and ports.
<p>The MKtlDesc for an OSC device has an info dictionary which can contain: <code class='code prettyprint lang-sc'>ipAddress, srcPort, recvPort, recvPort</code>. All of these elements are optional. Each of them are described in the following subsections.
<p>For a general discussion on OSC Communication, see this guide: <a href="./../Guides/OSC_communication.html">OSC Communication</a>.<h3><a class='anchor' name='ipAddress'>ipAddress</a></h3>

<p>The <code class='code prettyprint lang-sc'>ipAddress</code> field in the OSC device info is the ipAddress of the OSC device, so an incoming message will have this sending address. If the device is a physical device connected to your computer that uses a particular protocol that is decoded by another program to OSC messages (as is the case for the Manta), then the <code class='code prettyprint lang-sc'>ipAddress</code> is the <code class='code prettyprint lang-sc'>\localhost</code> or <code class='code prettyprint lang-sc'>"127.0.0.1"</code>.
<p>If the OSC device is its own device that connects to a local network (via WIFI or wired), then the device will have its own IP address, and it will be something like <code class='code prettyprint lang-sc'>"192.168.2.42"</code>.<pre class='code prettyprint lang-sc'>netAddrInfo: ( ipAddress: "127.0.0.1" ); // localhost example
netAddrInfo: ( ipAddress: "192.168.2.42" );</pre>

<p>If the OSC device is only sending data, you may leave the <code class='code prettyprint lang-sc'>ipAddress</code> undefined. Defining it however provides for an additional filter on the data - only messages from the defined <code class='code prettyprint lang-sc'>ipAddress</code> will be used. If you have several of the same devices, you will need to define the <code class='code prettyprint lang-sc'>ipAddress</code> for each to distinguish between the devices.<h3><a class='anchor' name='srcPort'>srcPort</a></h3>

<p>The <code class='code prettyprint lang-sc'>srcPort</code> defines where the OSC messages come from. This is different from the port that the device sends the OSC messages to (see <a href="#recvPort">recvPort</a>).
<p>Some programs use a fixed port for this, others use a random port each time the OSC device is started (e.g. Max/MSP does not allow you to set the port that is sent from and uses a different one each time).
<p>In case the OSC device uses a fixed port, you can use this to filter the data that comes in:<pre class='code prettyprint lang-sc'>// the Manta lib sends from 31417
netAddrInfo: ( srcPort: 31417 )</pre>
<h3><a class='anchor' name='recvPort'>recvPort</a></h3>

<p>SuperCollider is usually listening on port 57120, but takes another port if that one happens to be not available. You can check the current port by asking:<pre class='code prettyprint lang-sc'>NetAddr.langPort;</pre>

<p>If an OSC device wants to send OSC data to SuperCollider it needs to know the port to send to. As some OSC devices do not allow you to change the port to which they send, you can define a new port that SuperCollider opens to receive the data from this source:<pre class='code prettyprint lang-sc'>// example of the port to which MantaOSC sends its data
netAddrInfo: ( recvPort: 31416 );</pre>

<p>If you leave the <code class='code prettyprint lang-sc'>recvPort</code> undefined then SuperCollider's default port (<code class='code prettyprint lang-sc'>NetAddr.langPort</code>) is used, and you need to configure your OSC device to send to that port.<h3><a class='anchor' name='recvPort'>recvPort</a></h3>

<p>If the OSC device can also receive messages, you need to know on which port it listens. You can find this in the documentation of the device. For sending a message to an OSC device you need to know both the <code class='code prettyprint lang-sc'>ipAddress</code> and the <code class='code prettyprint lang-sc'>recvPort</code>. If the <code class='code prettyprint lang-sc'>recvPort</code> is left undefined, then the OSCMKtlDevice will use the same port as the <code class='code prettyprint lang-sc'>srcPort</code>. If the <code class='code prettyprint lang-sc'>ipAddress</code> is left undefined then the <code class='code prettyprint lang-sc'>"127.0.0.1"</code> is used.<pre class='code prettyprint lang-sc'>( netAddrInfo: ( srcPort: 31417, ipAddress: "127.0.0.1" ) );
m.device.source;
// will be NetAddr( "127.0.0.1", 31417 );
m.device.destination;
// will be NetAddr( "127.0.0.1", 31417 );


( netAddrInfo: ( srcPort: 31417, ipAddress: "127.0.0.1", recvPort: 31419 ) );
m.device.source;
// will be NetAddr( "127.0.0.1", 31417 );
m.device.destination;
// will be NetAddr( "127.0.0.1", 31419 );

( netAddrInfo: ( recvPort: 31419 ) );
m.device.source;
// will be nil;
m.device.destination;
// will be NetAddr( "127.0.0.1", 31419 );

// As the ipAddresses and ports may change in the network,
// one can update them dynamically:
m.device.updateSrcAddr("127.0.0.1", 12345);
m.device.updateDestAddr("127.0.0.1", 12345);</pre>
<h2><a class='anchor' name='OSC%20special%20messages'>OSC special messages</a></h2>

<p>Some devices respond to special messages; e.g. one can send a message to the Manta to tell it to enable outside control of its LEDs. You can define these messages in the <code class='code prettyprint lang-sc'>specialMessages</code> field of the device description. For OSC these have to be an Array of messages that need to be sent, each element of the Array contains the OSC path and the parameters that need to be sent.
<p>For example for the Manta we need:<pre class='code prettyprint lang-sc'>(
// ... other desc data
// ...
specialMessages: (
    enableLEDControl: [
        [ "/manta/ledcontrol", "padandbutton", 1 ],
        [ "/manta/ledcontrol", "slider", 1 ],
        [ "/manta/ledcontrol", "button", 1 ]
])
)</pre>
<h2><a class='anchor' name='argTemplate%20matching'>argTemplate matching</a></h2>

<p>This section gives some examples of how the <code class='code prettyprint lang-sc'>argTemplate</code> matching works in <a href="./../Classes/OSCdef.html">OSCdef</a> works. In MKtl the <code class='code prettyprint lang-sc'>argTemplate</code> follows the same principles.<pre class='code prettyprint lang-sc'>n = NetAddr.new( "127.0.0.1", NetAddr.langPort );

OSCdef( \test, { |msg| msg.postln; }, "/test", argTemplate: [ 0 ] );

n.sendMsg( "/test", 0, 2 ); // match
n.sendMsg( "/test", 1, 1 ); // no match

OSCdef( \test, { |msg| msg.postln; }, "/test", argTemplate: [ "hello" ] );

n.sendMsg( "/test", "hello", 2 ); // match
n.sendMsg( "/test", 1, 1 );       // no match

OSCdef( \test, { |msg| msg.postln; }, "/minibee/data", argTemplate: [ 7 ] );

n.sendMsg( "/minibee/data", 7, 0, 0, 0 ); // match
n.sendMsg( "/minibee/data", 6, 0, 0, 0 ); // no match


OSCdef( \test, { |msg| msg.postln; }, "/grid", argTemplate: [ 2, 3 ] );

n.sendMsg( "/grid", 2, 3, 0.4 ); // match
n.sendMsg( "/grid", 1, 2, 0.2 ); // no match

// multiple items to match at second item in template:
OSCdef( \test, { |msg| msg.postln; }, "/second", argTemplate: [ nil, [3,2] ] );

n.sendMsg( "/second", 2, 3, 0.4 ); // match
n.sendMsg( "/second", 2, 2, 0.4 ); // match
n.sendMsg( "/second", 1, 4, 0.2 ); // no match</pre>
<div class='doclink'>helpfile source: <a href='file:///home/stefan/.local/share/SuperCollider/downloaded-quarks/Modality-toolkit/Modality/HelpSource/Tutorials/How_to_create_a_description_file_for_OSC.schelp'>/home/stefan/.local/share/SuperCollider/downloaded-quarks/Modality-toolkit/Modality/HelpSource/Tutorials/How_to_create_a_description_file_for_OSC.schelp</a><br>link::Tutorials/How_to_create_a_description_file_for_OSC::<br>sc version: 3.9dev</div></div></body></html>