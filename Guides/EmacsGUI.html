<html><head><title>Emacs GUI</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider GUIDES</div>
<div id='categories'><a href='./../Browse.html#Frontends'>Frontends</a></div>
<h1>Emacs GUI</h1>
<div id='summary'>Emacs user interfaces</div>
</div>
<div class='subheader'>
</div>
<div id='toc'>
<ul class='toc'></ul></div>
<p>The Emacs interface actually has some nice options for user interfaces as well. Here are some examples:<pre class='code prettyprint lang-sc'>// create a buffer:
p = EmacsBuffer.new;

// show the buffer:
p.front;

// create a key action for the buffer:

p.defineKey( "hello", { "hey there".postln; } );

// type hello on the window and look at the postbuffer
p.front;

p.defineKey( "hey there", { "hello".postln; } );

// type hey there and look at the postbuffer
p.front;

// put some text in the buffer:

p.insert( "this is a really interesting text to read in the buffer" );
p.front;

// make a newline:

p.newline;

// make a button:

p.button( [ "on", "off", "in between" ], { |v| v.postln; } );
p.front;

// make a button with a different look:
p.button( [ "on", "off", "in between" ], { |v| v.postln; }, "******", "+++++" );
p.front;

// create a close button:
p.closeButton;
// clicking it will close the buffer!

p = EmacsBuffer.new;

p.editableField( "write something here", "like this?", { |v| v.postln; } );
p.front;

// moving the cursor around in the buffer:
p.gotoBob; // beginning
p.gotoEob; // end
p.goto( 5 ); // go to fifth position

// making it a sclang-mode buffer to write code in:
Emacs.evalLispExpression( p.use( [ 'sclang-mode' ] ).asLispString );

// close it:
p.free;

//

p = EmacsBuffer.new; // args: name
p.front;

// making a number box
n = EmacsNumber.new( p, "number box", [0,5].asSpec, { |v| v.postln; } ); // args: buffer, tag/label, spec, action
p.front;

// making a button:
x = EmacsButton( p, [ "on", "off", "in between" ], { |v| v.postln; }, "******", "+++++" );
// args: buffer, states, action, prefix, suffix
p.front;

t = EmacsText( p, "hello", 30 ); // args: buffer, string, size, align
p.front;

p.newline;
t = EmacsText( p, "helloooo", 30, \right ); // args: buffer, string, size, align

p.newline;
e = EmacsEditableField( p, "edit field", "edit me" ).action_( { |v| v.postln; } );
p.front;

b = EmacsPushButton( p, "hello" ).action_( { "do it".postln; } );
p.front;

// you can disable things:

b.enabled_( false );
p.front;</pre>

<p>And last but not least, it provides a class browser:<pre class='code prettyprint lang-sc'>EmacsClassBrowser.new( EmacsBuffer );</pre>
<div class='doclink'>helpfile source: <a href='file:///usr/local/share/SuperCollider/HelpSource/Guides/EmacsGUI.schelp'>/usr/local/share/SuperCollider/HelpSource/Guides/EmacsGUI.schelp</a><br>link::Guides/EmacsGUI::<br>sc version: 3.8dev</div></div></body></html>