<html><head><title>Constraints</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider GUIDES (extension)</div>
<div id='categories'><a href='./../Browse.html#Libraries>crucial>Constraints'>Libraries>crucial>Constraints</a></div>
<h1>Constraints</h1>
<div id='summary'>specify a condition or logical query, further filter and refine it</div>
</div>
<div class='subheader'>
</div>
<div id='toc'>
<ul class='toc'></ul></div>
<p>Constraints let you specify conditions in an OOP fashion. You can perform logical operations on the constraint object itself to further filter or refine your query.<pre class='code prettyprint lang-sc'>(
// Create a constraint.
c =
    Constraint({ arg obj; obj.even  })
        and: Constraint({ arg obj; obj % 4 == 0 })
        and: (Constraint({ arg obj; obj == 8 }).not);
)</pre>

<p>c is now a constraint object that can be used to validate that an input is even, divisible by 4 and is not the number 8.<pre class='code prettyprint lang-sc'>c.value(3)

c.value(8)

c.value(4)

c.value(12)</pre>

<p>This can be used any place a function that returns true/false is required. eg. select, reject, every, any<pre class='code prettyprint lang-sc'>(
// run numbers through it
50.do({
    n = 40.rand;
    [n,c.value(n)].postln
});
)</pre>

<p>It can be used in place of a function for SequenceableCollections, Streams and Patterns<pre class='code prettyprint lang-sc'>(
// filter a collection
Array.fill(100,{ 100.rand })
    .select(c) // acts like a function
    .do({ arg num; num.postln; });
)

(
// Use to filter a Pattern
p = Pseries(0,1,100)
    .select(c);
)

// Unfiltered
Pseries(0,1,100).asStream.all.do({arg num; num.postln;})

// Filtered
p.asStream.all.do({ arg num; num.postln });


(
// and here is everybody that gets rejected by the constraint
p = Pseries(0,1,100)
    .reject(c);
)

p.asStream.all.do({ arg num; num.postln });</pre>

<p>The below example is expressed using only Constraint.<pre class='code prettyprint lang-sc'>(
c =
    Constraint({ arg obj; obj.even  })
        and: Constraint({ arg obj; obj % 4 == 0 })
        and: (Constraint({ arg obj; obj == 8 }).not);
)</pre>

<p>It could also be expressed this way<pre class='code prettyprint lang-sc'>(
c =
    IsEven.new and:  Constraint({ arg obj; obj % 4 == 0 })
        and: Constraint({ arg obj; obj != 8 });
)</pre>

<p>Constraints respond to
<p> .not  .or(aConstraint)  .and(aConstraint)  .xor(aConstraint)  .reject(aConstraint)  .select(aConstraint)
<p>by returning a new compound constraint that expresses that logic.<pre class='code prettyprint lang-sc'>(
    c = IsEven.new;
    d = Constraint({ arg num; num == 3 });

    e = c or: d; // if its even or it is the number 3
)
(
    c = IsEven.new;
    d = Constraint({ arg num; num == 4 });

    e = c.reject(d); // if its even and also reject it if it is the number 4
)</pre>
<div class='doclink'>source: <a href='file:///home/stefan/.local/share/SuperCollider/downloaded-quarks/crucial-library/HelpSource/Guides/Constraints.schelp'>/home/stefan/.local/share/SuperCollider/downloaded-quarks/crucial-library/HelpSource/Guides/Constraints.schelp</a><br>link::Guides/Constraints::<br>sc version: 3.7alpha0</div></div></body></html>