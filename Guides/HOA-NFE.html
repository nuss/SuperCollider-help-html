<!doctype html><html lang='en'><head><title>Near-Field Effect (NFE) | SuperCollider 3.12.2 Help</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../codemirror.css' type='text/css' />
<link rel='stylesheet' href='./../editor.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta name='viewport' content='width=device-width, initial-scale=1'>
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../lib/jquery.min.js'></script>
<script src='./../lib/codemirror-5.39.2.min.js' type='text/javascript'></script>
<script src='./../lib/codemirror-addon-simple-5.39.2.min.js' type='text/javascript'></script>
<script>
var helpRoot = './..';
var scdoc_title = 'Near-Field Effect (NFE)';
var scdoc_sc_version = '3.12.2';
</script>
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='qrc:///qtwebchannel/qwebchannel.js' type='text/javascript'></script>
</head>
<body onload='fixTOC()'>
<div id='toc'>
<div id='toctitle'>Near-Field Effect (NFE):</div>
<span class='toc_search'>Filter: <input id='toc_search'></span><ul class='toc'><li class='toc1'><a href='#The%20Near-Field%20Effect'>The Near-Field Effect</a></li>
<ul class='toc'><li class='toc2'><a href='#FOA'>FOA</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#HOA'>HOA</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Encoding%20&%20decoding'>Encoding &amp; decoding</a></li>
<ul class='toc'><li class='toc2'><a href='#Encode%20a%20spherical%20wave'>Encode a spherical wave</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Sample%20a%20spherical%20wave'>Sample a spherical wave</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Beaming%20&%20nulling'>Beaming &amp; nulling</a></li>
<ul class='toc'><li class='toc2'><a href='#Beamform%20with%20HoaBeam'>Beamform with HoaBeam</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Beamform%20with%20HoaXformMatrix'>Beamform with HoaXformMatrix</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Nullform%20with%20HoaNull'>Nullform with HoaNull</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Techniques'>Techniques</a></li>
<ul class='toc'><li class='toc2'><a href='#Decode%20to%20a%202D%20array'>Decode to a 2D array</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Synthesize%20a%20diffuse%20field'>Synthesize a diffuse field</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Translate%20to%20a%20new%20radius'>Translate to a new radius</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Modulate%20at%20radius'>Modulate at radius</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Format%20Exchange'>Format Exchange</a></li>
<ul class='toc'><li class='toc2'><a href='#Encode%20from%20FOA'>Encode from FOA</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Decode%20to%20FOA'>Decode to FOA</a></li>
<ul class='toc'></ul></ul></ul></div><div id='menubar'></div>
<div class='contents'>
<div class='header'>
<div id='label'>
<span id='folder'>Guides (extension)</span>
 | <span id='categories'><a href='./../Browse.html#Libraries'>Libraries</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Libraries>Ambisonic Toolkit'>Ambisonic Toolkit</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Libraries>Ambisonic Toolkit>Guides & Tutorials'>Guides & Tutorials</a></span>
</div><h1>Near-Field Effect (NFE)<div class='extension-indicator-ctr' title='This help file originates from a third-party quark or plugin for SuperCollider.'><img class='extension-indicator-icon' alt='Extension' src='./../images/plugin.png'><span class='extension-indicator-text'>Extension</span></div></h1>
<div id='summary'>Higher Order Ambisonic (HOA) near-field effect examples</div>
</div>
<div class='subheader'>
</div>
<a class='anchor' name='kw_Atk'>&nbsp;</a>
<p>This document provides a very short overview of the near-field effect (NFE). A variety of simple HOA example implementations follow.<h2><a class='anchor' name='The%20Near-Field%20Effect'>The Near-Field Effect</a></h2>

<p>We'll by briefly reviewing the near-field effect (NFE) in the context of FOA and HOA.<h3><a class='anchor' name='FOA'>FOA</a></h3>

<p>The near-field effect (NFE) has been formally included within the Ambisonic framework since its initial design. In the classic days of Ambisonic hardware, users <em>usually</em> only directly interfaced this aspect of Ambisonics through the use of:<ul>
<li>the soundfiled microphone on the <em>encoding</em> side<li>Near-Field Compensation (NFC) loudspeaker distance correction filtering on the <em>decoding</em> side</ul>

<p>The ATK's FOA toolset includes NFE radial filters in their classic Ambisonic form. In the FOA context a <em>travelling wave</em> encoded with all real, aka <em>basic</em>, coefficients represents a <em>planewave</em>. The ATK also names this travelling wave as a <em>basic wave</em>, because of the use of basic, real coefficient encoding. SuperCollider's <a href="./../Classes/PanB.html">PanB</a> is a basic, planewave encoder.
<p>An Ambisonic system's <em>reference radius</em> is the radius at which a basic wave is encoded. So the <em>reference radius</em> locates the radius at which the system's encoding uses real numbers only.
<p>For FOA, the reference radius is infinity. A wave arriving from infinity is a <em>planewave</em>, which is why FOA's <em>basic wave</em> is a <em>planewave</em>.
<p>On receiving a basic wave, the FOA <a href="./../Classes/FoaProximity.html">proximity filter</a> add curvature to the wavefront, converting it from a planewave to a <em>spherical wave</em><a class='footnote anchor' name='footnote_org_1' href='#footnote_1'><sup>1</sup></a>  at a defined radius.<a class='footnote anchor' name='footnote_org_2' href='#footnote_2'><sup>2</sup></a> 
<p>The FOA <a href="./../Classes/FoaNFC.html">near-field compensation filter</a> returns the inverse of the FOA proximity filter, and given the same radial argument undoes the curvature to the wavefront that had been applied. In the context of loudspeaker decoding, the near-field compensation filter is intended to remove the wavefront add by the nearness of the loudspeakers. I.e., near-field compensation filter removes the curve added by the physical nearness of real monitors in the real world.
<p>When <a href="./../Classes/FoaNFC.html">FoaNFC</a> and <a href="./../Classes/FoaProximity.html">FoaProximity</a> are used together, we can reach inside a soundfield and <em>apply processing at a specified radius</em>. The one possible network looks like this:<div class='image'><img src='sourceextraction.png'/><br><b>Source extraction</b></div>

<p>Parameters <strong>theta</strong> and <strong>phi</strong> supply the look direction. The <strong>angle</strong> argument continuously transforms the image between:<ul>
<li>0 : original unchanged soundfield<li>pi/2 : spherical wave sampled at <strong>distance</strong></ul>

<p>The term <em>radial beamforming</em> is used to describe this soundfield operation. With radial beamforming the soundfield can be decomposed and recomposed as spherical waves, at whatever radius we choose.<h3><a class='anchor' name='HOA'>HOA</a></h3>

<p>It could be argued that the greatest innovation in Daniel's reframing of Ambisonics in a higher order context is the translation of the <em>basic wave</em> to a radius other than infinity.
<p>If we'd like to think in a real world way, this is equivalent to <em>pre-filtering</em> a soundfield with the near-field compensation filter in anticipation of decoding to loudspeakers located at a <em>pre-determined</em> radius. This radius is the <em>reference radius</em>. We can view this as the anticipated loudspeakers finishing off the synthesis of the curve of the encoded waves by physically adding the remaining curve.
<p>This is all good, but the true genius is the inclusion of a the <em>near-field effect control</em> filter, which combines the near-field compensation and proximity filter into a single block. The arrangement is as illustrated above, but without the inclusion of focus. Also, instead of having a single <em>distance</em> argument, there are two, one for each filter. Doing so makes it very easy to translate the reference radius. In other words, we can move where basic waves are encoded, which easily facilitates radial beamforming.
<p>If we prefer thinking in terms of virtual loudspeakers, changing the reference radius corresponds to moving the virtual loudspeakers. This then corresponds to moving the soundfield sampling radius when it comes to decomposition and recomposition.
<p>________________
<p>As part of the ATK's HOA toolset we have three principal filters tasked with the near-field effect.
<p>Each of these has a distinct role:<dl>
<dt>Near-Field Effect Distance filter (NFE-D)<dd><a href="./../Classes/HoaNFDist.html">HoaNFDist</a> transforms a <em>basic wave</em> into a <em>planewave</em>.
<p><a href="./../Classes/Signal.html#*hoaDist">Signal: *hoaDist</a> is the associated FIR kernel designer.<dt>Near-Field Effect Proximity filter (NFE-P)<dd><a href="./../Classes/HoaNFProx.html">HoaNFProx</a> transforms a <em>planewave</em> into a <em>basic wave</em>.
<p>Note, an associated FIR kernel designer is not provided.<a class='footnote anchor' name='footnote_org_3' href='#footnote_3'><sup>3</sup></a> <dt>Near-Field Effect Control filter (NFE-C)<dd><a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a> transforms a <em>basic wave</em> into a <em>spherical wave</em> at another radius.<a class='footnote anchor' name='footnote_org_4' href='#footnote_4'><sup>4</sup></a> 
<p><a href="./../Classes/Signal.html#*hoaCtrl">Signal: *hoaCtrl</a> is the associated FIR kernel designer.</dl>
<div class='note'><span class='notelabel'>NOTE:</span> In NFC-HOA encoding, <em>planewaves</em> and <em>spherical waves</em> are referenced to a real number valued reference radius.
<p>E.g., a <em>planewave</em> in NFC-HOA is not a <em>basic wave</em>. Conversly, complex coefficients are required to encode and synthesize a <em>planewave</em>.</div>
<p>Unlike their FOA equivalents, <a href="./../Classes/HoaNFDist.html">HoaNFDist</a> and <a href="./../Classes/HoaNFProx.html">HoaNFProx</a> <em>do not</em> have an exposed <strong>distance</strong> argument. By default, the <strong>distance</strong> argument is set internally to the ATK's reference radius for HOA, 1.5 meters.<a class='footnote anchor' name='footnote_org_5' href='#footnote_5'><sup>5</sup></a>  Doing so enforces the ATK's NFE encoding convention.
<p><a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a> has two arguments, <strong>encoding radius</strong> and <strong>decoding radius</strong>, which allows comprehensive control of the near-field effect in HOA.
<p>We'll review some examples, below.<h2><a class='anchor' name='Encoding%20&%20decoding'>Encoding &amp; decoding</a></h2>

<p>Two examples intended to offer insight into what encoded <em>spherical waves</em> look like with respect to signal phase and gain.<h3><a class='anchor' name='Encode%20a%20spherical%20wave'>Encode a spherical wave</a></h3>

<p>For this first example we'll encode a <em>travelling wave</em>. The three example radii all return <em>spherical waves</em>. One of these, encoded at 1.5 meters, is a <em>basic wave</em>.<div class='note'><span class='notelabel'>NOTE:</span> A decoder hasn't been provided, the intention of this example is to inspect the returned HOA encoded signals for a collection of spherical waves. You'll see that for an encoding at the reference radius, when all <em>coefficients are real</em>, all harmonics are <em>aligned in phase</em>.
<p>Observe this in the scope.</div><textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);

// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius &lt;-- basic wave
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;

    sig = Saw.ar(freq, gain.dbamp);

    HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Sample%20a%20spherical%20wave'>Sample a spherical wave</a></h3>

<p>By sampling a soundfield at the radius a <em>spherical wave</em> was originally encoded, we can recover the original source.
<p>This is called <em>radial soundfield sampling</em>. We look into the soundfield at a specific radius.
<p>The look direction of the beamformer, <a href="./../Classes/HoaDecodeDirection.html">HoaDecodeDirection</a>, is slowly moving back and forth. This movement is what is responsible for the varing gain of the returned beam.<div class='note'><span class='notelabel'>NOTE:</span> The three beamshape choice offer different shaped spatial sampling windows.</div><textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(2, ~outbus);  // view original signal &amp; mono decode

// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// beaming
~thetaRate = 0.125;  // modulation rate for beam look direction
~beamShape = \basic;
~beamShape = \energy;
~beamShape = \controlled;
~decRadius = ~encRadius; // explicitly set to match encoding radius


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;
    var hoa;
    var dec;

    sig = Saw.ar(freq, gain.dbamp);

    hoa = HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);

    dec = HoaDecodeDirection.ar(
        hoa,
        LFTri.kr(~thetaRate, mul: pi),
        ~phi,
        ~encRadius,
        ~beamShape,
        ~order
    );

    [ sig,  dec];

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h2><a class='anchor' name='Beaming%20&%20nulling'>Beaming &amp; nulling</a></h2>

<p>Beaming and nulling transforms are spatial bandpass and bandreject filters. Instead of operating in the frequency domain, we're operating in the spatial domain.
<p>Given the same beamshape, the beam summed with a corresponding null will return the original soundfield.
<p>Used in this way, these two transforms allow us to easily process different parts of the soundfield in different ways. E.g., the null could be lowpass filtered and then remixed with the beam.<div class='note'><span class='notelabel'>NOTE:</span> As with the soundfield sampler <a href="./../Classes/HoaDecodeDirection.html">HoaDecodeDirection</a> example <a href="#Sample%20a%20spherical%20wave">above</a>, the beam is formed at a sampling radius.</div><h3><a class='anchor' name='Beamform%20with%20HoaBeam'>Beamform with HoaBeam</a></h3>

<p>Encode six sinusoids on the axes, then sample the soundfield with a single beam.
<p>The beam is reencoded, so we're auditioning a virtual speaker at the beam sampling radius, which can be varied. We'll need a decoder to audition.
<p>This is like spatially bandpass filtering the soundfield.<textarea class='editor'>// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);

// design encoding matrix
~directions = [ [0.0, 0.0], [90.0, 0.0], [180.0, 0.0], [-90.0, 0.0], [0.0, 90.0], [0.0, -90.0], ].degrad;
~matrixEncoder = HoaMatrixEncoder.newDirections(~directions, order: ~order);

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// beaming
~thetaRate = 0.125;  // modulation rate for beam look direction
~phi = 0.0;
~beamShape = \basic;
~beamShape = \energy;
~beamShape = \controlled;


// freqs
~freqs = Array.series(~directions.size, 110.0, 110.0);
~amps = Array.series(~directions.size, 1, 1).reciprocal;

// audition
(
{
    var gain = -12;

    var sig, hoa;

    sig = SinOsc.ar(~freqs, mul: gain.dbamp * ~amps);

    hoa = HoaNFCtrl.ar(
        HoaEncodeMatrix.ar(sig, ~matrixEncoder),
        ~encRadius,  // encoding radius
        AtkHoa.refRadius,  // reference radius
        ~order
    );

    HoaBeam.ar(hoa, LFTri.kr(~thetaRate, mul: pi), ~phi, ~encRadius, ~beamShape, ~order);

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Beamform%20with%20HoaXformMatrix'>Beamform with HoaXformMatrix</a></h3>

<p>Similar to <a href="#Beamform%20with%20HoaBeam">Beamform with HoaBeam</a>, but this time beaming with a static matrix.<div class='note'><span class='notelabel'>NOTE:</span> Because the beaming transform matrix is a real number matrix, the beam is formed at the reference radius.
<p>To beam at another radius, we'd have to use the technique illustrated <a href="#Modulate%20at%20radius">below</a>.</div><textarea class='editor'>// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);

// design encoding matrix
~directions = [ [0.0, 0.0], [90.0, 0.0], [180.0, 0.0], [-90.0, 0.0], [0.0, 90.0], [0.0, -90.0], ].degrad;
~matrixEncoder = HoaMatrixEncoder.newDirections(~directions, order: ~order);

// design beaming matrix
~theta = 0.0;
~phi = 0.0;
~matrixXformer = HoaMatrixXformer.newBeam(~theta, ~phi, order: ~order);


// freqs
~freqs = Array.series(~directions.size, 110.0, 110.0);
~amps = Array.series(~directions.size, 1, 1).reciprocal;

// audition
(
{
    var gain = -12;

    var sig, hoa;

    sig = SinOsc.ar(~freqs, mul: gain.dbamp * ~amps);

    hoa = HoaEncodeMatrix.ar(sig, ~matrixEncoder);

    HoaXformMatrix.ar(hoa, ~matrixXformer);

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Nullform%20with%20HoaNull'>Nullform with HoaNull</a></h3>

<p><a href="#Beamform%20with%20HoaBeam">Same test as above</a>, but we form a null, instead. We are spatially rejecting part of the soundfield.<textarea class='editor'>// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);

// design encoding matrix
~directions = [ [0.0, 0.0], [90.0, 0.0], [180.0, 0.0], [-90.0, 0.0], [0.0, 90.0], [0.0, -90.0], ].degrad;
~matrixEncoder = HoaMatrixEncoder.newDirections(~directions, order: ~order);

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// nulling
~thetaRate = 0.125;  // modulation rate for null look direction
~phi = 0.0;
~beamShape = \basic;
~beamShape = \energy;
~beamShape = \controlled;


// freqs
~freqs = Array.series(~directions.size, 110.0, 110.0);
~amps = Array.series(~directions.size, 1, 1).reciprocal;

// audition
(
{
    var gain = -12;

    var sig, hoa;

    sig = SinOsc.ar(~freqs, mul: gain.dbamp * ~amps);

    hoa = HoaNFCtrl.ar(
        HoaEncodeMatrix.ar(sig, ~matrixEncoder),
        ~encRadius,  // encoding radius
        AtkHoa.refRadius,  // reference radius
        ~order
    );

    HoaNull.ar(hoa, LFTri.kr(~thetaRate, mul: pi), ~phi, ~encRadius, ~beamShape, ~order);

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h2><a class='anchor' name='Techniques'>Techniques</a></h2>

<p>The Near-Field Effect Control filter (NFE-C), <a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a> has a wide number of uses in HOA. Here are a few important examples.<h3><a class='anchor' name='Decode%20to%20a%202D%20array'>Decode to a 2D array</a></h3>

<p>Radial encoding followed by decoding with loudspeaker near-field compensation.
<p>When the real loudspeaker radius doesn't match the reference radius, we need to use <a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a> to reset the radial encoding.
<p>We can view this as <a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a> finishing the radial part of the panning law, to match our actual loudspeaker array.<div class='note'><span class='notelabel'>NOTE:</span> This is required if we would like the NFE to properly resolve.</div><textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// decoding - panto decoder
~numChans = 8;
~orientation = \flat;
~beamShape = \basic;
~beamShape = \energy;  // &lt;-- good choice!
~beamShape = \controlled;
~match = \amp;
~match = \rms;
~match = \energy;  // &lt;-- good choice!
~decRadius = 2.0;  // decoding / (actual) loudspeaker radius


// open scope
s.scope(~numChans, ~outbus);


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;
    var hoa;

    sig = Saw.ar(freq, gain.dbamp);

    // encode with NFE
    hoa = HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);

    // decode with NFE / loudspeaker near-field compensation
    HoaDecodeMatrix.ar(
        HoaNFCtrl.ar(  // NFE control
            hoa,
            AtkHoa.refRadius,
            ~decRadius,
            ~order
        ),
        HoaMatrixDecoder.newPanto(  // panto decoder
            ~numChans,
            ~orientation,
            ~beamShape,
            ~match,
            ~order
        )
    );

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Synthesize%20a%20diffuse%20field'>Synthesize a diffuse field</a></h3>

<p>Here we synthesize a soundfield that is diffuse at a specified encoding radius.<a class='footnote anchor' name='footnote_org_6' href='#footnote_6'><sup>6</sup></a> <textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);


// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 10.0;  // &gt;&gt; AtkHoa.refRadius
~encRadius = 5.0;  // &gt; AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// audition
(
{
    // params
    var gain = -12;

    var hoa;

    // diffuse field (synthesized at AtkHoa.refRadius)
    hoa = ~order.asHoaOrder.size.collect({ BrownNoise.ar(gain.dbamp) });

    // encode with NFE
    HoaNFCtrl.ar(  // NFE control
        hoa,
        ~encRadius,
        AtkHoa.refRadius,
        ~order
    );

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Translate%20to%20a%20new%20radius'>Translate to a new radius</a></h3>

<p>Translate from source radius to target radius. A very useful trick!<textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);


// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set (source) encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius

// target (translation) radius
~reEncRadius = 3.0;  // re-encoding radius &gt; AtkHoa.refRadius
~reEncRadius = 1.5;  // re-encoding radius = AtkHoa.refRadius
~reEncRadius = 1.0;  // re-encoding radius &lt; AtkHoa.refRadius


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;
    var hoa;

    sig = Saw.ar(freq, gain.dbamp);

    // encode with NFE
    hoa = HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);

    // re-encode NFE
    HoaNFCtrl.ar(
        hoa,
        ~reEncRadius,
        ~encRadius,
        ~order
    );

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Modulate%20at%20radius'>Modulate at radius</a></h3>

<p>The use of two <a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a> transformers offers the possibility of modulation at a given radius.
<p>The soundfield is decomposed into a collection of samples at a certain radius. These are then modulated. The soundfield is re-encoded, and samples are returned to their original radius.
<p>If we wish to granulate at a radius other than the reference radius, for instance, this example illustrates how to do so.<textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);


// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1
~order = 2
~order = 3

// choose an outbus, if need be
~outbus = 0

// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// decoding / re-encoding - spherical design (A/B-format)
~numChans = 24;
~beamShape = \basic;
~beamShape = \energy;
~beamShape = \controlled;
~sphDesign = TDesign.newHoa(~numChans, order: ~order);


// modulation
~modRate = 20.0;
~modRate = 10.0;
~modRate = 5.0;


// open scope
s.scope(~order.asHoaOrder.size, ~outbus);


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;
    var hoa;
    var aformat;

    sig = Saw.ar(freq, gain.dbamp);

    // encode with NFE
    hoa = HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);

    // decode with NFE
    aformat = HoaDecodeMatrix.ar(
        HoaNFCtrl.ar(  // NFE control - decode at encoding radius, the "look" radius
            hoa,
            AtkHoa.refRadius,
            ~encRadius,
            ~order
        ),
        HoaMatrixDecoder.newSphericalDesign(  // a-format decoder
            ~sphDesign,
            ~beamShape,
            ~order
        )
    );

    // modulate amplitude of "virtual loudspeakers" - in a-format
    aformat = aformat * ~numChans.collect({ LFNoise2.ar(~modRate).range });
    // aformat = aformat * ~numChans.collect({ LFNoise2.ar(~modRate) });

    // re-encode with NFE
    HoaNFCtrl.ar(  // NFE control - re-encode at encoding radius, the "look" radius
        HoaEncodeMatrix.ar(
            aformat,
            HoaMatrixEncoder.newSphericalDesign(  // a-format encoder
                ~sphDesign,
                ~beamShape,
                ~order
            )
        ),
        ~encRadius,
        AtkHoa.refRadius,
        ~order
    );

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h2><a class='anchor' name='Format%20Exchange'>Format Exchange</a></h2>

<p>If you haven't already, do review <a href="./../Guides/Format-Exchange.html">Ambisonic Format Exchange</a>.<h3><a class='anchor' name='Encode%20from%20FOA'>Encode from FOA</a></h3>

<p>The example using <a href="./../Classes/HoaNFDist.html">HoaNFDist</a> is equivalent to the one found <a href="./../Guides/Format-Exchange.html#encode%20HOA1%20from%20FOA">here</a>.<textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1  // match FOA, first order!

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);

// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;
    var foa;

    sig = Saw.ar(freq, gain.dbamp);  // saw
    // sig = SinOsc.ar(freq, mul: gain.dbamp);  // sine

    // FOA encoding, refRadius = inf
    foa = FoaProximity.ar(  // FOA radial encoding
        FoaPanB.ar(  // FOA angular encoding
            HPF.ar(sig, freq/3),  // precondition sig, i.e., "make safe" for proximity
            ~theta,
            ~phi
        ),
        ~encRadius
    );

    // HOA re-encoding, refRadius = AtkHoa.refRadius (= 1.5)
    HoaNFDist.ar(
        HoaEncodeMatrix.ar(
            foa,
            HoaMatrixEncoder.newFormat(\fuma, ~order)  // from fuma to atk
        ),
        ~order
    );

    // // compare to direct HOA1 encoding
    // HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);  // no need to precondition sig!

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>
<h3><a class='anchor' name='Decode%20to%20FOA'>Decode to FOA</a></h3>

<p>The example using <a href="./../Classes/HoaNFProx.html">HoaNFProx</a> is equivalent to the one found <a href="./../Guides/Format-Exchange.html#decode%20HOA3%20to%20FOA">here</a>.<textarea class='editor'>// Inspect the ATK's reference radius
"Reference radius: % m".format(AtkHoa.refRadius);

// simple test...

// boot rt server
s.boot;

// choose Ambisonic order
~order = 1  // match FOA, first order!

// choose an outbus, if need be
~outbus = 0

// open scope
s.scope(~order.asHoaOrder.size, ~outbus);

// encoding angles
// azimuth &amp; elevation
~theta = 0.0;
~phi = 0.0;

// explicitly set encoding radius
// i.e., radius != AtkHoa.refRadius
~encRadius = 2.0;  // &gt; AtkHoa.refRadius
~encRadius = 1.5;  // = AtkHoa.refRadius
~encRadius = 1.0;  // &lt; AtkHoa.refRadius


// audition
(
{
    // saw params
    var freq = 110.0;
    var gain = -12;

    var sig;
    var hoa;

    sig = Saw.ar(freq, gain.dbamp);  // saw
    // sig = SinOsc.ar(freq, mul: gain.dbamp);  // sine

    // HOA encoding, refRadius = AtkHoa.refRadius (= 1.5)
    hoa = HoaEncodeDirection.ar(sig, ~theta, ~phi, ~encRadius, ~order);  // no need to precondition sig!

    // FOA decoding, refRadius = inf
    HoaDecodeMatrix.ar(
        HoaNFProx.ar(
            HPF.ar(hoa, freq/3),  // precondition sig, i.e., "make safe" for proximity (HOA1)
            ~order
        ),
        HoaMatrixDecoder.newFormat(\fuma, ~order)  // from atk to fuma
    );

}.play(outbus: ~outbus)
)

// quit rt server
s.quit</textarea>

<p><div class='footnotes'>
<a class='anchor' name='footnote_1'/><div class='footnote'>[<a href='#footnote_org_1'>1</a>] - Many users will refer to this as an encoded <em>point source</em>.</div><a class='anchor' name='footnote_2'/><div class='footnote'>[<a href='#footnote_org_2'>2</a>] - <a href="./../Classes/FoaProximity.html">FoaProximity</a> argument <strong>distance</strong>.</div><a class='anchor' name='footnote_3'/><div class='footnote'>[<a href='#footnote_org_3'>3</a>] - Too many big numbers!</div><a class='anchor' name='footnote_4'/><div class='footnote'>[<a href='#footnote_org_4'>4</a>] - A spherical wave with complex coefficients.</div><a class='anchor' name='footnote_5'/><div class='footnote'>[<a href='#footnote_org_5'>5</a>] - If desired, the system reference radius may be changed: <a href="./../Classes/AtkHoa.html#*setRefRadius">AtkHoa: *setRefRadius</a>.</div><a class='anchor' name='footnote_6'/><div class='footnote'>[<a href='#footnote_org_6'>6</a>] - Arguably, one might consider a soundfield to be considered diffuse only if it is composed of <em>planewaves</em>. If that is your preference, then <a href="./../Classes/HoaNFDist.html">HoaNFDist</a> is the tool for the job.</div></div><div class='doclink'>helpfile source: <a href='file:///home/stefan/.local/share/SuperCollider/downloaded-quarks/atk-sc3/HelpSource/Guides/HOA-NFE.schelp'>/home/stefan/.local/share/SuperCollider/downloaded-quarks/atk-sc3/HelpSource/Guides/HOA-NFE.schelp</a><br>link::Guides/HOA-NFE::<br></div></div><script src='./../editor.js' type='text/javascript'></script>
</body></html>