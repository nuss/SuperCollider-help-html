<!doctype html><html lang='en'><head><title>Pantophonic Monitoring of HOA (2D) | SuperCollider 3.12.2 Help</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../codemirror.css' type='text/css' />
<link rel='stylesheet' href='./../editor.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta name='viewport' content='width=device-width, initial-scale=1'>
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../lib/jquery.min.js'></script>
<script src='./../lib/codemirror-5.39.2.min.js' type='text/javascript'></script>
<script src='./../lib/codemirror-addon-simple-5.39.2.min.js' type='text/javascript'></script>
<script>
var helpRoot = './..';
var scdoc_title = 'Pantophonic Monitoring of HOA (2D)';
var scdoc_sc_version = '3.12.2';
</script>
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='qrc:///qtwebchannel/qwebchannel.js' type='text/javascript'></script>
</head>
<body onload='fixTOC()'>
<div id='toc'>
<div id='toctitle'>Pantophonic Monitoring of HOA (2D):</div>
<span class='toc_search'>Filter: <input id='toc_search'></span><ul class='toc'><li class='toc1'><a href='#Analysis'>Analysis</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Quad'>Quad</a></li>
<ul class='toc'><li class='toc2'><a href='#Quad%20*newPanto%20(HOA1)'>Quad *newPanto (HOA1)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Quad%20*newPanto%20(HOA3)'>Quad *newPanto (HOA3)</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Octa'>Octa</a></li>
<ul class='toc'><li class='toc2'><a href='#Octa%20*newPanto%20(HOA1)'>Octa *newPanto (HOA1)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Octa%20*newPanto%20(HOA3)'>Octa *newPanto (HOA3)</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#7_0%20Surround'>7_0 Surround</a></li>
<ul class='toc'><li class='toc2'><a href='#7_0%20*newModeMatch%20(HOA3)'>7_0 *newModeMatch (HOA3)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#7_0%20*ADT%20(HOA3)'>7_0 *ADT (HOA3)</a></li>
<ul class='toc'></ul></ul></ul></div><div id='menubar'></div>
<div class='contents'>
<div class='header'>
<div id='label'>
<span id='folder'>Guides (extension)</span>
 | <span id='categories'><a href='./../Browse.html#Libraries'>Libraries</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Libraries>Ambisonic Toolkit'>Ambisonic Toolkit</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Libraries>Ambisonic Toolkit>Guides & Tutorials'>Guides & Tutorials</a></span>
</div><h1>Pantophonic Monitoring of HOA (2D)<div class='extension-indicator-ctr' title='This help file originates from a third-party quark or plugin for SuperCollider.'><img class='extension-indicator-icon' alt='Extension' src='./../images/plugin.png'><span class='extension-indicator-text'>Extension</span></div></h1>
<div id='summary'>Higher Order Ambisonic (HOA) 2D array decoding examples</div>
</div>
<div class='subheader'>
</div>
<a class='anchor' name='kw_Atk'>&nbsp;</a>
<p>The ATK offers HOA loudspeaker decoding via two methods:<dl>
<dt>projection<dd><em>Projection</em> decoders require equally distributed loudspeakers. This is easy for 2D decoding, as all we need to do is to arrange loudspeakers in a polygon.
<p>The ATK's HOA 2D <em>projection decoder</em> is <a href="./../Classes/HoaMatrixDecoder.html#*newPanto">HoaMatrixDecoder: *newPanto</a>.<dt>mode matching<dd>The loudspeakers for <em>mode matching</em> decoders are not required to be equally distributed, however, the more evenly sampled an array is, the better the result.
<p>The ATK includes two <em>mode matching decoders</em>: <a href="./../Classes/HoaMatrixDecoder.html#*newModeMatch">HoaMatrixDecoder: *newModeMatch</a> &amp; <a href="./../Classes/HoaMatrixDecoder.html#*newDiametric">HoaMatrixDecoder: *newDiametric</a>.</dl>
<div class='note'><span class='notelabel'>NOTE:</span> If the <a href="./../Tutorials/ABCs-of-the-ATK.html#ADT">ADT quark</a> has been installed, the <a href="https://bitbucket.org/ambidecodertoolbox/adt/src/master/">Ambisonic Decoder Toolbox</a> can be used to return decoders designed via other methods.</div>
<p>________________
<p>In the examples to follow, we'll restrict ourselves to designing matrices with the assumption that all loudspeakers are equally distant from the array origin.<h2><a class='anchor' name='Analysis'>Analysis</a></h2>

<p>Before we start, evaluate the plotting functions found here:<ul>
<li><a href="./../Classes/HoaMatrixDecoder.html#Equatorial%20rV%20&%20rE">rV &amp; rE</a>: <code>~rVrEequator</code><li><a href="./../Classes/HoaMatrixDecoder.html#Equatorial%20rV%20&%20rE%20error">rV &amp; rE error</a>: <code>~rVrEdistortEquator</code><li><a href="./../Classes/HoaMatrixDecoder.html#Equatorial%20energy%20spread">energy spread</a>: <code>~spreadEquator</code><li><a href="./../Classes/HoaMatrixDecoder.html#Equatorial%20amplitude%20and%20energy">amplitude and energy</a>: <code>~ampEnergyEquator</code></ul>

<p>We'll use these to evaluate the performance of the designed decoders.<h2><a class='anchor' name='Quad'>Quad</a></h2>

<p>Our most simple system for 2D decoding. If you haven't reviewed the discussion of quadraphonic panning in the context of pairwise <a href="./../Tutorials/ATK-Enlightenment.html#Panorama%20Laws">panning laws</a>, this would be a good time to do so.<h3><a class='anchor' name='Quad%20*newPanto%20(HOA1)'>Quad *newPanto (HOA1)</a></h3>

<p>Here will use <a href="./../Classes/HoaMatrixDecoder.html#*newPanto">HoaMatrixDecoder: *newPanto</a> to design a suitable matrix decoder for HOA1:<textarea class='editor'>(
// decode: HOA1 &gt;&gt; quad

// ---------
// Pantophonic (2D) decoder with four channels arranged in a regular polygon
//
// single band

// order
~order = 1;

// specify parameters &amp; design
~numChans = 4;

// beam shape (panning law)
// ~beamShape = \basic;
~beamShape = \energy;
// ~beamShape = \controlled;

// gain match
// ~match = \amp;
// ~match = \rms;
~match = \energy;

~decoder = HoaMatrixDecoder.newPanto(~numChans, beamShape: ~beamShape, match: ~match, order: ~order);
)</textarea>

<p><strong>Performance plots</strong>
<p>Let's view the performance by inspecting a few analysis plots.
<p>Start with:<textarea class='editor'>~rVrEequator.value(~decoder)</textarea>

<p>When we choose the <em>energy optimised</em> beam shape, the <em>rV</em> and <em>rE</em> vectors are the same length.
<p>For an equally sampled array, we can predict the length of these vectors, which are measures of how localized a portion of the soundfield will appear in terms of <em>velocity</em> and <em>energy</em>.
<p>You'll notice that we're seeing circles plotted. The soundfield is equally resolved in all directions.
<p>Theoretical average values can be found:<textarea class='editor'>~hoaOrder = ~decoder.order.asHoaOrder;
~hoaOrder.rV(~beamShape, ~decoder.dim).round(0.01);
~hoaOrder.rE(~beamShape, ~decoder.dim).round(0.01);</textarea>

<p>Read these as a fraction of what we would expect for a real sound. As the values approach one, the phantom image matches the measure for a sound in the real world.
<p>Try some of the other beam shapes and observe the plots and measures.
<p>Let's try some other plots:<textarea class='editor'>~rVrEdistortEquator.value(~decoder);  // angular distortion: rV &amp; rE
~spreadEquator.value(~decoder);  // image spread
~ampEnergyEquator.value(~decoder);  // amplitude and energy</textarea>

<p>These plots illustrate the preformance of the decoder in terms of:<ol>
<li>localisation vector angular distortion<li>phantom image spread<li>amplitude and energy across the array</ol>

<p>Front center is located at the center of the plots.
<p>The first thing we're looking for in these plots is equal values in all directions. That's what we're seeing!
<p>In particular, we see:<ol>
<li><em>No angular distortion</em>. Phantom images will appear where they were intended. (That's panning!)<li><em>Even image energy spread</em>, which is associate with image resolution.<li><em>Amplitude and energy remain constant</em> across the array. No direction appears louder than another.</ol>

<p><strong>Network</strong>
<p>We should finish the radial part of the panning law with <a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a>. And to generate loudspeaker feeds, we'll need <a href="./../Classes/HoaDecodeMatrix.html">HoaDecodeMatrix</a> to render:<textarea class='editor'>~decRadius = 2.0;  // decoding / (actual) loudspeaker radius

// decode with NFE / loudspeaker near-field compensation
~myQuad = HoaDecodeMatrix.ar(
    HoaNFCtrl.ar(  // NFE control
        ~myHoa1,
        AtkHoa.refRadius,
        ~decRadius,
        ~decoder.order  // ~order
    ),
    ~decoder
);</textarea>
<h3><a class='anchor' name='Quad%20*newPanto%20(HOA3)'>Quad *newPanto (HOA3)</a></h3>

<p>When we design a decoder a quadraphonic decoder for HOA3, surprisingly we'll see that the performance is the same as that seen for HOA1. The reasons for this are discussed in <a href="./../Tutorials/ATK-Enlightenment.html">Ambisonic Enlightenment</a>.
<p>We can review the rule for <a href="./../Tutorials/ATK-Enlightenment.html#Array%20resolution">2D array resolution</a>, to be clear.
<p>Or... we can just use <a href="./../Classes/HoaMatrixDecoder.html#*newPanto">HoaMatrixDecoder: *newPanto</a> to design a matrix decoder for HOA3. What we'll see is that the returned HOA3 quad decoder has the same performance at the previous <a href="#Quad%20(HOA1)">HOA1 quad decoder</a>:<textarea class='editor'>(
// decode: HOA3 &gt;&gt; quad

// ---------
// Pantophonic (2D) decoder with four channels arranged in a regular polygon
//
// single band

// order
~order = 3;

// specify parameters &amp; design
~numChans = 4;

// beam shape (panning law)
// ~beamShape = \basic;
~beamShape = \energy;
// ~beamShape = \controlled;

// gain match
// ~match = \amp;
// ~match = \rms;
~match = \energy;

~decoder = HoaMatrixDecoder.newPanto(~numChans, beamShape: ~beamShape, match: ~match, order: ~order);
)</textarea>

<p><strong>Performance plots</strong>
<p>Feel free to run the test plots:<textarea class='editor'>~rVrEequator.value(~decoder);

~rVrEdistortEquator.value(~decoder);  // angular distortion: rV &amp; rE
~spreadEquator.value(~decoder);  // image spread
~ampEnergyEquator.value(~decoder);  // amplitude and energy</textarea>

<p>We'll return the same results as earlier.
<p>We can also compare measured results to theory:<textarea class='editor'>// angular sampling rate
~angularOrder = ((~decoder.numChannels / 2) - 1).ceil.asInteger;  // 1st order
~angularHoaOrder = ~angularOrder.asHoaOrder;

// analysis (100 test directions)
~analysis = ~decoder.analyzeDirections(Array.regularPolygon(100));

// compare
~angularHoaOrder.rV(~beamShape, ~decoder.dim).equalWithPrecision(
    ~analysis.rV.magnitudes.mean,  // analyzed rV.mean
    0.00001
);

~angularHoaOrder.rE(~beamShape, ~decoder.dim).equalWithPrecision(
    ~analysis.rE.magnitudes.mean,  // analyzed rE.mean
    0.00001
);</textarea>

<p><strong>Network</strong>
<p>In practice, we can recognize that truncating an HOA3 by hand will be more efficient for an undersample array:<textarea class='editor'>~decRadius = 2.0;  // decoding / (actual) loudspeaker radius

~angularOrder = ((~numChans / 2) - 1).ceil.asInteger;  // 1st order &lt;- array order
~angularHoaOrder = ~angularOrder.asHoaOrder;

// decode with NFE / loudspeaker near-field compensation
~myQuad = HoaDecodeMatrix.ar(
    HoaNFCtrl.ar(  // NFE control
        ~myHoa3.keep(~angularHoaOrder.size),  // truncate to HOA1
        AtkHoa.refRadius,
        ~decRadius,
        ~angularOrder  // &lt;- array order
    ),
    ~decoder  // THIS SHOULD BE QUAD for HOA1 -- see above!
);</textarea>
<div class='note'><span class='notelabel'>NOTE:</span> We don't have to truncate by hand if we don't want to. An HOA3 quad decoder will truncate inside the returned matrix.
<p>If we truncate by hand we can save render time by using a lower order <a href="./../Classes/HoaNFCtrl.html">HoaNFCtrl</a>.</div><h2><a class='anchor' name='Octa'>Octa</a></h2>

<p>For eight equally distributed loudspeakers, we'll also use <a href="./../Classes/HoaMatrixDecoder.html#*newPanto">HoaMatrixDecoder: *newPanto</a>.
<p>For this example we'll see that an octaphonic decoder for HOA3 performs much better than the equivalent for HOA1. The physical array is sampled high enough to return more resolved performance.<h3><a class='anchor' name='Octa%20*newPanto%20(HOA1)'>Octa *newPanto (HOA1)</a></h3>
<textarea class='editor'>(
// decode: HOA1 &gt;&gt; octa

// ---------
// Pantophonic (2D) decoder with four channels arranged in a regular polygon
//
// single band

// order
~order = 1;

// specify parameters &amp; design
~numChans = 8;
~orientation = \vertex;

// beam shape (panning law)
// ~beamShape = \basic;
~beamShape = \energy;
// ~beamShape = \controlled;

// gain match
// ~match = \amp;
// ~match = \rms;
~match = \energy;

~decoder = HoaMatrixDecoder.newPanto(~numChans, ~orientation, ~beamShape, ~match, ~order);
)</textarea>

<p><strong>Performance plots</strong>
<p>Feel free to run the test plots:<textarea class='editor'>~rVrEequator.value(~decoder);

~rVrEdistortEquator.value(~decoder);  // angular distortion: rV &amp; rE
~spreadEquator.value(~decoder);  // image spread
~ampEnergyEquator.value(~decoder, 360, -5.0, 10.0);  // amplitude and energy</textarea>

<p>Here we see all the performance measures are the same as for the HOA1 quad example, except for the amplitude measure. This is uniform, but it differs from the quad measurement. The reason for this is that we've matched the gain performance for <em>energy</em>:<textarea class='editor'>~decoder.analyzeAverage.energy.ampdb;</textarea>

<p>As earlier, we see:<ol>
<li><em>No angular distortion</em><li><em>Even image energy spread</em><li><em>Constant amplitude and energy</em></ol>

<p><strong>Network</strong>
<p>The completed render network... same as for HOA1 quad, but for the octa decoder:<textarea class='editor'>~decRadius = 2.0;  // decoding / (actual) loudspeaker radius

// decode with NFE / loudspeaker near-field compensation
~myOcta = HoaDecodeMatrix.ar(
    HoaNFCtrl.ar(  // NFE control
        ~myHoa1,
        AtkHoa.refRadius,
        ~decRadius,
        ~decoder.order  // ~order
    ),
    ~decoder
);</textarea>
<h3><a class='anchor' name='Octa%20*newPanto%20(HOA3)'>Octa *newPanto (HOA3)</a></h3>

<p>An octaphonic array meets the <a href="./../Tutorials/ATK-Enlightenment.html#Array%20resolution">array resolution</a> required to reproduce 2D HOA3. (Hurrah!)<textarea class='editor'>~order = 3;
~numChans = 8;

// loudspeaker array order
~angularOrder = ((~numChans / 2) - 1).ceil.asInteger;  // 3rd order

// does it match?
~order == ~angularOrder;</textarea>

<p>Here's the design code:<textarea class='editor'>(
// decode: HOA3 &gt;&gt; octa

// ---------
// Pantophonic (2D) decoder with four channels arranged in a regular polygon
//
// single band

// order
~order = 3;

// specify parameters &amp; design
~numChans = 8;

// beam shape (panning law)
// ~beamShape = \basic;
~beamShape = \energy;
// ~beamShape = \controlled;

// gain match
// ~match = \amp;
// ~match = \rms;
~match = \energy;

~decoder = HoaMatrixDecoder.newPanto(~numChans, beamShape: ~beamShape, match: ~match, order: ~order);
)</textarea>

<p>Feel free to run the test plots:<textarea class='editor'>~rVrEequator.value(~decoder);

~rVrEdistortEquator.value(~decoder);  // angular distortion: rV &amp; rE
~spreadEquator.value(~decoder);  // image spread
~ampEnergyEquator.value(~decoder);  // amplitude and energy</textarea>

<p>With all beam shapes (panning laws!) we see very good performance. As earlier, measures are even across the array. Also, the localisation vectors are long.
<p>Compare measured results to theory:<textarea class='editor'>// angular sampling rate
~angularOrder = ((~decoder.numChannels / 2) - 1).ceil.asInteger;  // 3rd order
~angularHoaOrder = ~angularOrder.asHoaOrder;

// analysis (100 test directions)
~analysis = ~decoder.analyzeDirections(Array.regularPolygon(100));

// compare
~angularHoaOrder.rV(~beamShape, ~decoder.dim).equalWithPrecision(
    ~analysis.rV.magnitudes.mean,  // analyzed rV.mean
    0.00001
);

~angularHoaOrder.rE(~beamShape, ~decoder.dim).equalWithPrecision(
    ~analysis.rE.magnitudes.mean,  // analyzed rE.mean
    0.00001
);

// and rV &amp; rE magnitudes - measured
~analysis.rV.magnitudes.mean;
~analysis.rE.magnitudes.mean;</textarea>

<p><strong>Network</strong>
<p>Since the spatial sampling of the octa array meets HOA3, we won't truncate to save CPU:<div class='note'><span class='notelabel'>NOTE:</span> The earlier quad code wouldn't end up truncating, anyway, because we were doing a test.</div><textarea class='editor'>~decRadius = 2.0;  // decoding / (actual) loudspeaker radius

// decode with NFE / loudspeaker near-field compensation
~myOcta = HoaDecodeMatrix.ar(
    HoaNFCtrl.ar(  // NFE control
        ~myHoa3,
        AtkHoa.refRadius,
        ~decRadius,
        ~decoder.order  // or, as this is Atk.defaultOrder, we can skip this arg
    ),
    ~decoder
);</textarea>
<h2><a class='anchor' name='7_0%20Surround'>7_0 Surround</a></h2>

<p>Given an array of unevenly distributed loudspeakers, <a href="./../Classes/HoaMatrixDecoder.html#*newModeMatch">HoaMatrixDecoder: *newModeMatch</a> is the decoder to choose.
<p>What we'll see is the uneven array sampling leads to uneven performance measures.
<p>For this example we'll see what happens with a less ideal array.<h3><a class='anchor' name='7_0%20*newModeMatch%20(HOA3)'>7_0 *newModeMatch (HOA3)</a></h3>

<p>The ATK's mode matcher is not especially sophisticated when it comes to mode or degree truncation. The simple test we've seen before for <a href="./../Tutorials/ATK-Enlightenment.html#Array%20resolution">2D</a> or 3D array resolution is made. For reasonably distributed arrays, we can get a good result.
<p>We'll always want to review the returned performance to see if an acceptable decoder is returned.
<p>Design:<textarea class='editor'>(
// decode: HOA3 &gt;&gt; 7_0

// ---------
// Pantophonic (2D) decoder with seven channels arranged in a consumer orientation
//
// single band

// order
~order = 3;

// specify parameters &amp; design
~directions = [ 0.0, 30.0, 110.0, 135.0, -135.0, -110.0, -30.0 ].degrad;  // 7.0 array
// ~directions = [ 30.0, 110.0, 135.0, -135.0, -110.0, -30.0 ].degrad;  // 7.0 array - discard center!

// beam shape (panning law)
// ~beamShape = \basic;
~beamShape = \energy;
// ~beamShape = \controlled;

// gain match
// ~match = \amp;
// ~match = \rms;
~match = \energy;

~decoder = HoaMatrixDecoder.newModeMatch(~directions, ~beamShape, ~match, ~order);
)</textarea>

<p>Go ahead and run the test plots:<textarea class='editor'>~rVrEequator.value(~decoder);

~rVrEdistortEquator.value(~decoder, 360, 30);  // angular distortion: rV &amp; rE
~spreadEquator.value(~decoder);  // image spread
~ampEnergyEquator.value(~decoder, 360, -5, 20);  // amplitude and energy</textarea>

<p>If you've kept the plot open for octa HOA3 with <em>energy optimised</em> beam, you'll see that the <em>energy optimised</em> 7_0 design returns fairly comparable performance within the front sixty degrees of the array.
<p>However, as we move away from the front, the performance degrades.<div class='note'><span class='notelabel'>NOTE:</span> If we discard the center loudspeaker, we get a bit of a more even performance.
<p>(Keep the plots up and design a new decoder w/out the center.)
<p>Part of what is happening here is that the mode matcher is truncating from HOA3 to HOA2, as we have six loudspeakers rather then seven when the center is discarded.</div>
<p>If we try different orders and different beam shapes, we'll see that the performance varies significantly!
<p>We can seek to quantify the performance of this designed decoder by inspecting:<textarea class='editor'>// analysis
(
~numTestDirections = 100;

~analysis = ~decoder.analyzeDirections(
    Array.regularPolygon(~numTestDirections)
);

~hoaOrder = HoaOrder.new(~order);  // required for ideal measures
)

// amp
(
"---".postln;
"Amplitude range: % ~ % dB".format(
    ~analysis.amp.minItem.ampdb.round(0.01),
    ~analysis.amp.maxItem.ampdb.round(0.01)
).postln;
"Amplitude mean: % dB".format(
    ~analysis.amp.mean.ampdb.round(0.01)
).postln;
"Amplitude median: % dB".format(
    ~analysis.amp.median.ampdb.round(0.01)
).postln;
)

// energy
(
"---".postln;
"Energy range: % ~ % dB".format(
    ~analysis.energy.minItem.ampdb.round(0.01),
    ~analysis.energy.maxItem.ampdb.round(0.01)
).postln;
"Energy mean: % dB".format(
    ~analysis.energy.mean.ampdb.round(0.01)
).postln;
"Energy median: % dB".format(
    ~analysis.energy.median.ampdb.round(0.01)
).postln;
)

// rV error (warping)
(
"---".postln;
"rV error range: % ~ % deg".format(
    ~analysis.rV.warp.minItem.raddeg.round(0.01),
    ~analysis.rV.warp.maxItem.raddeg.round(0.01)
).postln;
"rV error mean: % deg".format(
    ~analysis.rV.warp.mean.raddeg.round(0.01)
).postln;
"rV median: % deg".format(
    ~analysis.rV.warp.median.raddeg.round(0.01)
).postln;
)

// rE error (warping)
(
"---".postln;
"rE error range: % ~ % deg".format(
    ~analysis.rE.warp.minItem.raddeg.round(0.01),
    ~analysis.rE.warp.maxItem.raddeg.round(0.01)
).postln;
"rE error mean: % deg".format(
    ~analysis.rE.warp.mean.raddeg.round(0.01)
).postln;
"rE median: % deg".format(
    ~analysis.rE.warp.median.raddeg.round(0.01)
).postln;
)

// rV &amp; rE alignment
(
"---".postln;
"rV &amp; rE alignment range: % ~ % deg".format(
    ~analysis.rV.rEwarp.minItem.raddeg.round(0.01),
    ~analysis.rV.rEwarp.maxItem.raddeg.round(0.01)
).postln;
"rV &amp; rE alignment mean: % deg".format(
    ~analysis.rV.rEwarp.mean.raddeg.round(0.01)
).postln;
"rV &amp; rE alignment median: % deg".format(
    ~analysis.rV.rEwarp.median.raddeg.round(0.01)
).postln;
)

// rV magnitude
(
"---".postln;
"|rV| ideal: %".format(
    ~hoaOrder.rV(~beamShape, ~decoder.dim).round(0.01)
).postln;
"---".postln;
"|rV| range: % ~ %".format(
    ~analysis.rV.magnitudes.minItem.round(0.01),
    ~analysis.rV.magnitudes.maxItem.round(0.01)
).postln;
"|rV| mean: % deg".format(
    ~analysis.rV.magnitudes.mean.round(0.01)
).postln;
"|rV| median: % deg".format(
    ~analysis.rV.magnitudes.median.round(0.01)
).postln;
)

// rE magnitude
(
"---".postln;
"|rE| ideal: %".format(
    ~hoaOrder.rE(~beamShape, ~decoder.dim).round(0.01)
).postln;
"---".postln;
"|rE| range: % ~ %".format(
    ~analysis.rE.magnitudes.minItem.round(0.01),
    ~analysis.rE.magnitudes.maxItem.round(0.01)
).postln;
"|rE| mean: % deg".format(
    ~analysis.rE.magnitudes.mean.round(0.01)
).postln;
"|rE| median: % deg".format(
    ~analysis.rE.magnitudes.median.round(0.01)
).postln;
)

// energy spread (cos)
(
"---".postln;
"Energy spread ideal: % deg".format(
    ~hoaOrder.rE(~beamShape, ~decoder.dim).cos.raddeg.round(0.01)
).postln;
"---".postln;
"Energy spread range: % ~ % deg".format(
    ~analysis.spreadE.cos.minItem.raddeg.round(0.01),
    ~analysis.spreadE.cos.maxItem.raddeg.round(0.01)
).postln;
"Energy spread mean: % deg".format(
    ~analysis.spreadE.cos.mean.raddeg.round(0.01)
).postln;
"Energy spread median: % deg".format(
    ~analysis.spreadE.cos.median.raddeg.round(0.01)
).postln;
)</textarea>

<p>What we see is that the energy performance varies!
<p>This isn't surprising, as the mode matcher is trying to make up for the missing loudspeakers that are not found in the array.
<p><strong>Network</strong>
<p>Truncation happens inside the matrix, so we can just do the usual:<textarea class='editor'>~decRadius = 2.0;  // decoding / (actual) loudspeaker radius

// decode with NFE / loudspeaker near-field compensation
~my7_0 = HoaDecodeMatrix.ar(
    HoaNFCtrl.ar(  // NFE control
        ~myHoa3,
        AtkHoa.refRadius,
        ~decRadius,
        ~decoder.order  // or, as this is Atk.defaultOrder, we can skip this arg
    ),
    ~decoder
);</textarea>
<h3><a class='anchor' name='7_0%20*ADT%20(HOA3)'>7_0 *ADT (HOA3)</a></h3>

<p>For highly irregular loudspeaker arrays, other decoder design methods beyond simple mode matching are more likely to return more results. If we've installed the <a href="./../Tutorials/ABCs-of-the-ATK.html#ADT">Ambisonic Decoder Toolbox</a>, we'll have access to some of these.
<p>Let's review an example for a 2D design.
<p>First, we need to be sure to set local paths for ADT:<textarea class='editor'>(
ADT.userADTDir_("~/Documents/Developer/adt/".standardizePath);
ADT.userOctaveCmd_("/Applications/Octave-4.4.1.app/Contents/Resources/usr/bin/octave");
)</textarea>
<div class='note'><span class='notelabel'>NOTE:</span> We might want to consider adding these lines to the <a href="./../Reference/StartupFile.html">sclang startup file</a></div>
<p>We will start by specifying our decoder features and creating an ADT instance. In order to correctly manage the target gain normalisation, we'll treat the decoder returned by the ADT as a prototype. We'll then normalise the returned matrix later.<textarea class='editor'>(
// decode: HOA3 &gt;&gt; 7_0

// ---------
// Pantophonic (2D) decoder with seven channels arranged in a consumer orientation
//
// single band

// order
~order = 3;

// specify parameters &amp; design
~directions = [ 0.0, 30.0, 110.0, 135.0, -135.0, -110.0, -30.0 ].degrad;  // 7.0 array
// ~directions = [ 30.0, 110.0, 135.0, -135.0, -110.0, -30.0 ].degrad;  // 7.0 array - discard center!

// beam shape (panning law)
// ~beamShape = \basic;
~beamShape = \energy;
// ~beamShape = \controlled;

// gain match
// ~match = \amp;
// ~match = \rms;
~match = \energy;

// name
~name = "new7_0";  // final decoder name
~adtName = "%ADT".format(~name);  // ADT prototype name

// create instance of ADT
~adt = ADT.new(
    folderPath: nil,  // writes to ATK extensions
    directions: ~directions,
    match: \amp,  // reset match later
    order: ~order,
    format: AtkHoa.format,
    filename: ~adtName,
    ymlOnly: true // write only yml to the folderPath, the rest is written to the ADT repo folder
)
)</textarea>

<p>For a 2D design we have two design method choices: mode matching and AllRAD.<a class='footnote anchor' name='footnote_org_1' href='#footnote_1'><sup>1</sup></a> 
<p>If the ADT has been successfully started, we'll see this message in the post window: <code>ADT initialized.</code><ol>
<li>Mode matching, aka pseudoinverse<textarea class='editor'>// pinv decoder
(
~kind = "pinv";

// ~alpha = 0.0; // mode matching
~alpha = 0.5; // half &amp; half
// ~alpha = 1.0; // even energy - often better performance!

~adt.pinv(~alpha);
)</textarea>

<p><div class='note'><span class='notelabel'>NOTE:</span> The ADT's mode matcher includes an <strong>alpha</strong> parameter, which adjusts between directional accuracy and uniform loudness.</div><li>AllRAD<textarea class='editor'>(
~kind = "allrad";

~imagSpeakers = [[ 0, -0.5pi, 1.5 ], [ 0, 0.5pi, 1.5 ]]; // use 2 imaginary for 2D

~adt.allrad(~imagSpeakers)
)</textarea>

<p><div class='note'><span class='notelabel'>NOTE:</span> AllRAD takes time!</div></ol>

<p>If design of the prototype is successful, ADT will return: <code>Result = 0</code>.
<p>To finish off the design, we'll do the following:<ol>
<li>load the prototype decoder created by the ADT<li>create a new decoder and normalize to our chosen matching gain<li>write out this new decoder, for later use</ol>
<textarea class='editor'>(
// -----------
// 1) load prototype decoder
~adtFileName = "%-%-beam-%-match-%".format(
    ~adtName,
    ~kind,
    ~beamShape,
    \amp  // reset match later
);
~adtDecoder = HoaMatrixDecoder.newFromFile(~adtFileName, order: ~order);

// -----------
// 2) create new decoder, normalized against match
~decoder = HoaMatrixDecoder.newFromMatrix(
    ~adtDecoder.matrix * ~adtDecoder.analyzeAverage.matchWeight[~beamShape],
    ~adtDecoder.directions,
    ~adtDecoder.order
);

// -----------
// 3) prepare to write...
~adtProperties = ~adtDecoder.fileParse;  // adt properties

// .. with these properties (metadata)
~properties = ~adtProperties.deepCopy;

~properties.removeAt(\beam);  // discard ..
~properties.put(\beamShape, ~adtProperties[\beam]);  // .. to rename
~properties.put(\match, ~match);  // reset
~properties.put(\adtKind, ~adtDecoder.kind.asString.drop(3).asSymbol);

~properties = ~properties ++ (
    author: "ADT",
    dateCreated: Date.getDate.stamp,
);

// A 'note': a description or note about the matrix.
~note = "This is a 7.0 surround decoder made as part of Pantophonic Monitoring of HOA (2D) - ADT example.";

// ... now write
~decoder.writeToFile( "%.yml".format(~name),
    note: ~note,
    attributeDictionary: ~properties,
    overwrite: true
)
)</textarea>

<p>Hurrah!
<p>Given a successful design, we can run the same tests illustrated above: <a href="#7_0%20*newModeMatch%20(HOA3)">7_0 *newModeMatch (HOA3)</a>.<div class='footnotes'>
<a class='anchor' name='footnote_1'/><div class='footnote'>[<a href='#footnote_org_1'>1</a>] - The ADT doesn't appear to discard tesseral and zonal harmonics when designing mode matching or AllRad matrices, as would be expected for 2D designs.</div></div><div class='doclink'>helpfile source: <a href='file:///home/stefan/.local/share/SuperCollider/downloaded-quarks/atk-sc3/HelpSource/Guides/HOA-2D-Decoding.schelp'>/home/stefan/.local/share/SuperCollider/downloaded-quarks/atk-sc3/HelpSource/Guides/HOA-2D-Decoding.schelp</a><br>link::Guides/HOA-2D-Decoding::<br></div></div><script src='./../editor.js' type='text/javascript'></script>
</body></html>