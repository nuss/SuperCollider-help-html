<html><head><title>SynthDef and NRT examples for ATK</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider GUIDES (extension)</div>
<div id='categories'><a href='./../Browse.html#Libraries>Ambisonic Toolkit'>Libraries>Ambisonic Toolkit</a></div>
<h1>SynthDef and NRT examples for ATK<div class='extension-indicator-ctr' title='This help file originates from a third-party quark or plugin for SuperCollider.'><img class='extension-indicator-icon' alt='Extension' src='./../images/plugin.png'><span class='extension-indicator-text'>Extension</span></div></h1>
<div id='summary'>SynthDef and NRT examples for ATK</div>
</div>
<div class='subheader'>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#ATK%20with%20SynthDef%20and%20Synth'>ATK with SynthDef and Synth</a></li>
<ul class='toc'><li class='toc2'><a href='#Mono%20Encoder%20using%20FoaPanB'>Mono Encoder using FoaPanB</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Omni%20Encoder%20using%20FoaEncoderMatrix,%20Transforms%20using%20FoaTransform'>Omni Encoder using FoaEncoderMatrix, Transforms using FoaTransform</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Route%20Encoding%20Synth%20to%20a%20separate%20Decoding%20Synth'>Route Encoding Synth to a separate Decoding Synth</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Kernel%20Decoders%20and%20Encoders'>Kernel Decoders and Encoders</a></li>
<ul class='toc'><li class='toc2'><a href='#B-format%20Sound%20File%20and%20Binaural%20Decoder'>B-format Sound File and Binaural Decoder</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Encode%20an%20Ambisonic%20UHJ%20Stereo%20File,%20Decode%20to%20HRTF'>Encode an Ambisonic UHJ Stereo File, Decode to HRTF</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#ATK%20in%20Non-Realtime'>ATK in Non-Realtime</a></li>
<ul class='toc'><li class='toc2'><a href='#ATK%20and%20Score'>ATK and Score</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#ATK%20and%20the%20Composer's%20Toolkit%20(Ctk)'>ATK and the Composer's Toolkit (Ctk)</a></li>
<ul class='toc'></ul></ul></ul></div><a class='anchor' name='kw_Atk'>&nbsp;</a><h2><a class='anchor' name='ATK%20with%20SynthDef%20and%20Synth'>ATK with SynthDef and Synth</a></h2>

<p>For a more in-depth overview of the paradigm of ATK and a complete presentation of its capabilities, see <a href="./../Guides/Intro-to-the-ATK.html">Introducing the Ambisonic Toolkit</a>. These examples show a limited set of the ATK's functionality, but illustrate how to work with the library when using <a href="./../Classes/SynthDef.html">SynthDefs</a> and <a href="./../Classes/Synth.html">Synths</a>, and a single decoder that reads the Ambisonic signal through audio bus routing. Additionally, examples for <a href="./../Guides/Non-Realtime-Synthesis.html">Non-Realtime (NRT)</a> processing are found in this guide.<h3><a class='anchor' name='Mono%20Encoder%20using%20FoaPanB'>Mono Encoder using FoaPanB</a></h3>

<p><a href="./../Classes/FoaPanB.html">FoaPanB</a> encodes a monophonic input to a first order ambisonic signal (B-format), as a planewave. <a href="./../Classes/PanB.html">PanB</a> is the SuperCollider inbuilt equivalent.
<p>This first example encodes a <a href="./../Classes/PinkNoise.html">PinkNoise</a> source as a planewave and decodes to stereo.<pre class='code prettyprint lang-sc'>Server.default = s = Server.local.boot;

(
var decoder;

// First we will define our decoder
// stereo decoder
decoder = FoaDecoderMatrix.newStereo((131/2).degrad, 0.5);

// next we define a synth using FoaPanB, and decoder using FoaDecode
SynthDef(\foaEncode1, {
    var src, theta, phi, foa, out;

    // our source: pink noise
    src = PinkNoise.ar(-6.dbamp);

    // theta is our angle on the X-Y plane and phi is our elevation
    // use a MouseX to control theta in real time, from pi to -pi
    theta = MouseX.kr(pi, -pi);
    phi = 0;

    // Encode into our foa signal
    foa = FoaPanB.ar(src, theta, phi);

    // decode our signal using our decoder defined above
    out = FoaDecode.ar(foa, decoder);

     Out.ar(0, out);
}).add;
)

// play the synth
a = Synth(\foaEncode1);

//free the synth
a.free;</pre>
<h3><a class='anchor' name='Omni%20Encoder%20using%20FoaEncoderMatrix,%20Transforms%20using%20FoaTransform'>Omni Encoder using FoaEncoderMatrix, Transforms using FoaTransform</a></h3>

<p>Encodes a monophonic input as an <a href="./../Classes/FoaEncoderMatrix.html#*newOmni">omnidirectional</a> soundfield, then re-image via two transforms using the <a href="./../Classes/FoaTransform.html">FoaTransform</a> UGen wrapper.<pre class='code prettyprint lang-sc'>(
var decoder, encoder;
// First we will define our decoder and encoder
// stereo decoder
decoder = FoaDecoderMatrix.newStereo((131/2).degrad, 0.5);

// a matrix for an omni image
encoder = FoaEncoderMatrix.newOmni;

// define a synth using FoaEncode and FoaDecode
SynthDef(\foaEncode2, {
    var src, angle, azim, foa, out;

    // our source:  Pink Noise (could be any mono signal)
    src = PinkNoise.ar(-6.dbamp);


    // for the 'push' transform later
    // see FoaPush help for details
    // angle ---&gt; top           = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);


    // for 'rotate' transform
    // azimuth -&gt; hard left     = back
    //            centre        = centre
    //            hard right    = back
    azim = MouseX.kr(pi, -pi);


    // Encode into our foa signal
    foa = FoaEncode.ar(src, encoder);


    // push transform using angle
    foa = FoaTransform.ar(foa, 'pushX', angle);

    // rotate transform using azim
    foa = FoaTransform.ar(foa, 'rotate', azim);


    // decode our signal
    out = FoaDecode.ar(foa, decoder);


     Out.ar(0, out);
}).add;

)

// play the synth
a = Synth(\foaEncode2);

// free the synth
a.free;</pre>
<h3><a class='anchor' name='Route%20Encoding%20Synth%20to%20a%20separate%20Decoding%20Synth'>Route Encoding Synth to a separate Decoding Synth</a></h3>

<p>Encode a planewave, and route to a single decoder:<pre class='code prettyprint lang-sc'>(
var decoder;

// define our deocder
decoder = FoaDecoderMatrix.newStereo((131/2).degrad, 0.5);

// allocate four channels for routing
a = Bus.audio(s, 4);

// Encoding Synth
SynthDef(\foaEncode3, {arg outBus, duration = 0.05, theta, phi;
    var src, foa, env;

    // our mono source
    src = PinkNoise.ar(-6.dbamp);

    // amplitude scaling envelope
    env = EnvGen.kr(
            Env([0, 1, 0], [0.5, 0.5], \sin),
            timeScale: duration,
            doneAction: 2);

    // Encode into our foa signal
    foa = FoaPanB.ar(src, theta, phi, env);

    Out.ar(outBus, foa);
}).add;

// Decoding Synth
SynthDef(\foaDecode, {arg inBus;
    var foa, out;

    // read in 4 channels (B-format) from inBus
    foa = In.ar(inBus, 4);

    // decode to stereo
    out = FoaDecode.ar(foa, decoder);

     Out.ar(0, out);
}).add;

)


// start the decoder, reading bus 'a' at the \tail
b = Synth(\foaDecode, [\inBus, a], 1, \addToTail);

// use a Routine to start many encoded signals at random angles
Routine.run({
    20.do({
        Synth(\foaEncode3, [\outBus, a, \theta, pi.rand2, \phi, 0]);
        0.1.wait;
    })
});


b.free; // free the decoder
a.free; // free the audio bus</pre>
<h2><a class='anchor' name='Kernel%20Decoders%20and%20Encoders'>Kernel Decoders and Encoders</a></h2>
<h3><a class='anchor' name='B-format%20Sound%20File%20and%20Binaural%20Decoder'>B-format Sound File and Binaural Decoder</a></h3>

<p>In this example we're working with a B-format sound file. As the source is already encoded, an encoding stage is not needed. For audition, a <a href="./../Classes/FoaDecoderKernel.html#*newListen">binaural (HRTF) decoder</a> is used. This decoder takes a subjectID as an argument. It would be wise to experiment with various subjectIDs to discover which suits your own head.<pre class='code prettyprint lang-sc'>(
var cond, decoder, sndbuf, synth;

// boot the server
s.boot;

// wait for the server to boot
cond = Condition.new;
s.waitForBoot({


    Routine.run({

        // define a binaural decoder
        decoder = FoaDecoderKernel.newListen(1013);

        // load sound file into a buffer
        sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Pampin-On_Space.wav");

        s.sync(cond);

        // synth to decode our B-format sound file
        SynthDef(\kernelDecode, {arg buffer;
            var out, src;

            // play B-format sound file
            src = PlayBuf.ar(sndbuf.numChannels, buffer, BufRateScale.kr(buffer), loop: 1);

            // decode using decoder
            out = FoaDecode.ar(src, decoder);


            Out.ar(0, out);
        }).add;

        s.sync(cond);

        synth = Synth(\kernelDecode, [\buffer, sndbuf]);

        // press command period when done
        CmdPeriod.doOnce({
            synth.free;
            decoder.free;
            sndbuf.free
        });
    })
})
)</pre>
<h3><a class='anchor' name='Encode%20an%20Ambisonic%20UHJ%20Stereo%20File,%20Decode%20to%20HRTF'>Encode an Ambisonic UHJ Stereo File, Decode to HRTF</a></h3>

<p><a href="https://en.wikipedia.org/wiki/Ambisonic_UHJ_format">Ambisonic UHJ</a> is the stereo compatible Ambisonic format, and a suitable Ambisonic B-format signal can be retrieved from UHJ encoded signals.<a class='footnote anchor' name='footnote_org_1' href='#footnote_1'><sup>1</sup></a> 
<p>Here we will encode (<em>transcode</em>, actually!) a UHJ Stereo file to B-format. For audition, a <a href="./../Classes/FoaDecoderKernel.html#*newListen">binaural (HRTF) decoder</a> is used. This decoder takes a subjectID as an argument. It would be wise to experiment with various subjectIDs to discover which suits your own head. suits your own head.<pre class='code prettyprint lang-sc'>(
var cond, encoder, decoder, sndbuf, synth;

// boot the server
s.boot;

// wait for the server to boot
cond = Condition.new;
s.waitForBoot({

    Routine.run({

        // define an UHJ encoder
        encoder = FoaEncoderKernel.newUHJ;

        // define an HRTF decoder
        decoder = FoaDecoderKernel.newListen(1013);

        // load a UHJ sound file into a buffer
        sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/uhj/Palestrina-O_Bone.wav");

        s.sync(cond);

        // synth to encode a UHJ file and decode using an HRTF
        SynthDef(\kernelEncodeDecode, {arg buffer;
            var out, src, encode;

            // our stereo source signal
            src = PlayBuf.ar(sndbuf.numChannels, buffer, BufRateScale.kr(buffer));

            // encode using a UHJ encoder
            encode = FoaEncode.ar(src, encoder);

            //  decode using an HRTF decoder
            out = FoaDecode.ar(encode, decoder);

            Out.ar(0, out);
        }).add;

        s.sync(cond);

        // play the synth
        synth = Synth(\kernelEncodeDecode, [\buffer, sndbuf]);

        // press command period when done
        CmdPeriod.doOnce({
            synth.free;
            encoder.free;
            decoder.free;
            sndbuf.free});
    })
})
)</pre>
<h2><a class='anchor' name='ATK%20in%20Non-Realtime'>ATK in Non-Realtime</a></h2>
<h3><a class='anchor' name='ATK%20and%20Score'>ATK and Score</a></h3>

<p>In many cases, examples using <a href="./../Classes/Score.html">Score</a> are often trickier due to the need for the use of <a href="./../Guides/Bundled-Messages.html">bundles</a>. Since the Kernels<a class='footnote anchor' name='footnote_org_2' href='#footnote_2'><sup>2</sup></a>  also pass in hardcoded buffer IDs, we need to make sure those are referenced, as well.
<p>The example below decodes a B-format input file to Stereo Ambisonic UHJ using the ATK's <a href="./../Classes/FoaDecoderKernel.html#*newUHJ">FoaDecoderKernel: *newUHJ</a> decoder.<pre class='code prettyprint lang-sc'>(
var score, bufnum, sndPath, duration, decoder, sampleRate, headerFormat, sampleFormat, numChannels;
var offset = 0.1;

// deinfe our score
score = Score.new;

// get a buffer number from the server
bufnum = Server.default.bufferAllocator.alloc(1);

// the path to our B-Format sound file
sndPath = Atk.userSoundsDir ++ "/b-format/Pampin-On_Space.wav";

// get some info about the soundfile we are decoding for the Score requirements
SoundFile.use(
    sndPath,
    {arg soundFile;
        headerFormat = soundFile.headerFormat;
        sampleFormat = soundFile.sampleFormat;
        sampleRate = soundFile.sampleRate;
        numChannels = soundFile.numChannels;
        duration = soundFile.duration;
    }
);

// define a decoder of your choosing
// the decoder takes a score argument so that it will add the kernels to the score for you
decoder = FoaDecoderKernel.newUHJ(
    sampleRate: sampleRate,
    score: score
);

// define an encoding and decoding synth
SynthDef(\kernelDecode, {arg buffer;
    var out, src;

    // play B-format sound file from a buffer
    src = PlayBuf.ar(numChannels, buffer, BufRateScale.kr(buffer));

    // decode our B-format signal
    out = FoaDecode.ar(src, decoder);

    Out.ar(0, out);
}).load;

score.add(
    [ 0.0,
        [ 'b_allocRead', bufnum, sndPath, 0, 0 ],
        [ 's_new', 'kernelDecode', 1001, 0, 1, 'buffer', bufnum ]
    ],
);

// add commands to free the synth and buffer
score.add([ duration, [ 'n_free', 1001 ] ],);
score.add([ duration + 0.1, [ 'b_free', bufnum ] ],);

// free the kernel buffers
decoder.kernel.do({arg bufs;
    bufs.do({arg buf;
        offset = offset + 0.1;
        score.add([ duration  + offset, [ 'b_free', buf.bufnum ]])
    });
});

// add the needed dummy command to stop NRT
score.add([offset + duration + 0.2, [0]] );

// render our score to a sound file
score.recordNRT(
    "/tmp/trashme",
    "~/Desktop/myDecode.wav".standardizePath,
    sampleRate: sampleRate,
    headerFormat: headerFormat,
    sampleFormat: sampleFormat,
    options: ServerOptions.new.numOutputBusChannels_(decoder.numChannels)
);

)</pre>
<h3><a class='anchor' name='ATK%20and%20the%20Composer's%20Toolkit%20(Ctk)'>ATK and the Composer's Toolkit (Ctk)</a></h3>

<p>The <a href="https://github.com/supercollider-quarks/Ctk">Composer's Toolkit</a> (Ctk) <a href="./../Guides/UsingQuarks.html">quark</a> offers a convenient model for working in a score based paradigm in both Realtime and Non-Realtime.
<p>The example here, <a href="#ATK%20and%20Score">as above</a>, decodes a B-format input file to Stereo Ambisonic UHJ using the ATK's <a href="./../Classes/FoaDecoderKernel.html#*newUHJ">FoaDecoderKernel: *newUHJ</a> decoder.<pre class='code prettyprint lang-sc'>(
var score, sndbuf, sndPath, decoder, synth, duration, sampleRate, headerFormat, sampleFormat, numChannels;

// define our CtkScore
score = CtkScore.new;

// the path to our B-Format sound file
sndPath = Atk.userSoundsDir ++ "/b-format/Pampin-On_Space.wav";

// get some info about the soundfile we are decoding for the Score requirements
SoundFile.use(
    sndPath,
    {arg soundFile;
        headerFormat = soundFile.headerFormat;
        sampleFormat = soundFile.sampleFormat;
        sampleRate = soundFile.sampleRate;
        numChannels = soundFile.numChannels;
        duration = soundFile.duration;
    }
);

// define a CtkBuffer and add it to our score
sndbuf = CtkBuffer.playbuf(sndPath).addTo(score);

// define a decoder of your choosing
// the decoder takes a score argument so that it will add the kernels to the score for you
decoder = FoaDecoderKernel.newUHJ(
    sampleRate: sampleRate,
    score: score
);

// define a CtkSynthDef
synth = CtkSynthDef(\kernelDecode, {arg buffer;
    var out, src;

    // play a sound file from a buffer
    src = PlayBuf.ar(numChannels, buffer, BufRateScale.kr(buffer));

    // decode our B-format sound file
    out = FoaDecode.ar(src, decoder);

    Out.ar(0, out);
});

// create a synth note and add it to the score
score.add(
    synth.note(0.0, duration).buffer_(sndbuf)
);

// write our score to disk
score.write("~/Desktop/myDecode.wav".standardizePath,
    sampleRate: sampleRate,
    headerFormat: headerFormat,
    sampleFormat: sampleFormat,
    options: ServerOptions.new.numOutputBusChannels_(decoder.numChannels)
);
)</pre>

<p>And, this example uses both a Kernel <a href="./../Classes/FoaEncoderKernel.html">Encoder</a> and <a href="./../Classes/FoaDecoderKernel.html">Decoder</a>. First an <a href="https://en.wikipedia.org/wiki/Ambisonic_UHJ_format">Ambisonic UHJ</a> soundfile is encode to B-format. Then, it is decoded using the ATK's <a href="./../Classes/FoaDecoderKernel.html#*newListen">binaural (HRTF) decoder</a> decoder.<pre class='code prettyprint lang-sc'>(
var score, sndbuf, sndPath, encoder, decoder, synth, duration, sampleRate, headerFormat, sampleFormat, numChannels;

// define our CtkScore
score = CtkScore.new;

// the path to our B-Format sound file
sndPath = Atk.userSoundsDir ++  "/uhj/Palestrina-O_Bone.wav";

// get some info about the soundfile we are decoding for the Score requirements
SoundFile.use(
    sndPath,
    {arg soundFile;
        headerFormat = soundFile.headerFormat;
        sampleFormat = soundFile.sampleFormat;
        sampleRate = soundFile.sampleRate;
                numChannels = soundFile.numChannels;
        duration = soundFile.duration;
    }
);

// define a CtkBuffer and add it to our score
sndbuf = CtkBuffer.playbuf(sndPath).addTo(score);

// define the UHJ encoder
// the decoder takes a score argument so that it will add the kernels to the score for you
encoder = FoaEncoderKernel.newUHJ(
    sampleRate: sampleRate,
    score: score
);

// define a decoder of your choosing
// the decoder takes a score argument so that it will add the kernels to the score for you
decoder = FoaDecoderKernel.newListen(
    subjectID: 1013,
    sampleRate: sampleRate,
    score: score
);

// define a CtkSynthDef
synth = CtkSynthDef(\kernelEncodeDecode, {arg buffer;
    var out, encoded, src;

    // play a sound file from a buffer
    src = PlayBuf.ar(numChannels, buffer, BufRateScale.kr(buffer));

    // encode our UHJ sound file
    encoded = FoaEncode.ar(src, encoder);

    // decode our B-format sound file
    out = FoaDecode.ar(encoded, decoder);

    Out.ar(0, out);
});

// create a synth note and add it to the score
score.add(
    synth.note(0.0, duration).buffer_(sndbuf)
);

// write our score to disk
score.write("~/Desktop/myDecode.wav".standardizePath,
    sampleRate: sampleRate,
    headerFormat: headerFormat,
    sampleFormat: sampleFormat,
    options: ServerOptions.new.numOutputBusChannels_(decoder.numChannels)
);
)</pre>

<p><div class='footnotes'>
<a class='anchor' name='footnote_1'/><div class='footnote'>[<a href='#footnote_org_1'>1</a>] - See further discussion <a href="https://en.wikipedia.org/wiki/Ambisonic_UHJ_format">here</a>.</div><a class='anchor' name='footnote_2'/><div class='footnote'>[<a href='#footnote_org_2'>2</a>] - <a href="./../Classes/FoaEncoderKernel.html">FoaEncoderKernel</a> and <a href="./../Classes/FoaDecoderKernel.html">FoaDecoderKernel</a></div></div><div class='doclink'>helpfile source: <a href='file:///usr/local/share/SuperCollider/Extensions/SC3plugins/ATK/HelpSource/Guides/ATK-SynthDef-Examples.schelp'>/usr/local/share/SuperCollider/Extensions/SC3plugins/ATK/HelpSource/Guides/ATK-SynthDef-Examples.schelp</a><br>link::Guides/ATK-SynthDef-Examples::<br>sc version: 3.9dev</div></div></body></html>