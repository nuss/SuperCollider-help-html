<!doctype html><html lang='en'><head><title>Decoding FOA | SuperCollider 3.11.2 Help</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../codemirror.css' type='text/css' />
<link rel='stylesheet' href='./../editor.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta name='viewport' content='width=device-width, initial-scale=1'>
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../lib/jquery.min.js'></script>
<script src='./../lib/codemirror-5.39.2.min.js' type='text/javascript'></script>
<script src='./../lib/codemirror-addon-simple-5.39.2.min.js' type='text/javascript'></script>
<script>
var helpRoot = './..';
var scdoc_title = 'Decoding FOA';
var scdoc_sc_version = '3.11.2';
</script>
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='qrc:///qtwebchannel/qwebchannel.js' type='text/javascript'></script>
</head>
<body onload='fixTOC()'>
<div id='toc'>
<div id='toctitle'>Decoding FOA:</div>
<span class='toc_search'>Filter: <input id='toc_search'></span><ul class='toc'><li class='toc1'><a href='#Monitoring'>Monitoring</a></li>
<ul class='toc'><li class='toc2'><a href='#Testing%20notes'>Testing notes</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Stereo'>Stereo</a></li>
<ul class='toc'><li class='toc2'><a href='#Virtual%20microphone'>Virtual microphone</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Ambisonic%20UHJ'>Ambisonic UHJ</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Binaural'>Binaural</a></li>
<ul class='toc'><li class='toc2'><a href='#Synthetic'>Synthetic</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#CIPIC'>CIPIC</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#2D%20surround'>2D surround</a></li>
<ul class='toc'><li class='toc2'><a href='#Quadraphonic'>Quadraphonic</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#DecodeB2'>DecodeB2</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Optimised%20quadraphonic'>Optimised quadraphonic</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#ITU%205.0'>ITU 5.0</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#3D%20surround'>3D surround</a></li>
<ul class='toc'><li class='toc2'><a href='#Cube'>Cube</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Optimised%20bi-rectangle'>Optimised bi-rectangle</a></li>
<ul class='toc'></ul></ul></ul></div><div id='menubar'></div>
<div class='contents'>
<div class='header'>
<div id='label'>
<span id='folder'>Guides (extension)</span>
 | <span id='categories'><a href='./../Browse.html#Libraries'>Libraries</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Libraries>Ambisonic Toolkit'>Ambisonic Toolkit</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Libraries>Ambisonic Toolkit>Guides & Tutorials'>Guides & Tutorials</a></span>
</div><h1>Decoding FOA<div class='extension-indicator-ctr' title='This help file originates from a third-party quark or plugin for SuperCollider.'><img class='extension-indicator-icon' alt='Extension' src='./../images/plugin.png'><span class='extension-indicator-text'>Extension</span></div></h1>
<div id='summary'>First Order Ambisonic (FOA) decoding examples</div>
</div>
<div class='subheader'>
</div>
<a class='anchor' name='kw_Atk'>&nbsp;</a><h2><a class='anchor' name='Monitoring'>Monitoring</a></h2>

<p>Perhaps one of the most celebrated aspects of the Ambisonic sound technique has been its design as a hierarchal reproduction system, able to target a number of varying loudspeaker arrays. Users may be familiar with SuperCollider's inbuilt regular polygon decoder, <a href="./../Classes/DecodeB2.html">DecodeB2</a>.
<p>The ATK provides a much wider palate of optimised monitoring tools via <a href="./../Classes/FoaDecode.html">FoaDecode</a>. These include:<ul>
<li><em>stereo UHJ:</em> classic Ambisonic stereo decoding<li><em>binaural:</em> measured and synthetic HRTFs<li><em>regular 2D &amp; 3D:</em> single and dual polygons<li><em>diametric 2D &amp; 3D:</em> flexible semi-regular arrays<li><em>5.0:</em> <a href="http://www.brucewiggins.co.uk/">Wiggins</a> optimised decoders</ul>

<p>While the <em>regular</em> decoders will be suitable for many users, <em>diametric</em> decoding enables the greatest flexibility, and allows the user to design substantially varying semi-regular arrays suitable for a wide variety of playback situations.
<p>The examples below are intended to briefly illustrate some of the first order decoding options made available in the Ambisonic Toolkit.<div class='note'><span class='notelabel'>NOTE:</span> All decoders presume loudspeakers are placed at equal radii from the origin of the array, and gain is normalised. Loudspeaker arrays with unequal radii may be realised if the resulting wavefront arrival times are compensated through the use of delay lines.</div><div class='note'><span class='notelabel'>NOTE:</span> See <a href="./../Classes/FoaDecode.html">FoaDecode</a>, <a href="./../Classes/FoaDecoderMatrix.html">FoaDecoderMatrix</a> and <a href="./../Classes/FoaDecoderKernel.html">FoaDecoderKernel</a> for more details about decoding.</div><h3><a class='anchor' name='Testing%20notes'>Testing notes</a></h3>

<p>Encoded as an omnidirectional soundfield, <a href="./../Classes/PinkNoise.html">PinkNoise</a> is used as the example sound source. In a well aligned, dampend studio environment, this usually sounds "in the head". <a href="./../Classes/FoaPush.html">FoaPush</a> is used to "push" the omnidirectional soundfield so that it becomes a planewave (infinite distance, in an anechoic environment) arriving from some direction.
<p>The soundfield is controlled by <a href="./../Classes/MouseX.html">MouseX</a> and <a href="./../Classes/MouseY.html">MouseY</a>, where <a href="./../Classes/MouseX.html">MouseX</a> specifies the incident azimuth angle (pi to -pi; left to right of display) and <a href="./../Classes/MouseY.html">MouseY</a> the <a href="./../Classes/FoaPush.html">FoaPush</a> angle (0 to pi/2; bottom to top of display). With the mouse at the bottom of the display, the soundfield remains omnidirectional. Placed at the top of the display, the soundfield becomes directional, and varying left/right position will vary the incident azimuth of the resulting planewave.
<p>Before exploring the examples below, it is suggested you confirm your <a href="./../Classes/Server.html">Server</a> has enough output channels to support your chosen decoder. You can query the server:<textarea class='editor'>myServer.options.numOutputBusChannels</textarea>

<p>An example <a href="./../Classes/Function.html">Function</a>, <code>~checkMyServerOutputs</code>, can be found <a href="./../Guides/Intro-FOA-Examples.html#Choose%20a%20decoder">here</a>. <code>~checkMyServerOutputs</code> throws a warning if <code>myServer.options.numOutputBusChannels &lt; myDecoder.numOutputs</code>. If you need to update your <a href="./../Classes/Server.html">Server</a>'s number of output bus channels, review the example found <a href="./../Classes/ServerOptions.html#examples">here</a>.<h2><a class='anchor' name='Stereo'>Stereo</a></h2>
<h3><a class='anchor' name='Virtual%20microphone'>Virtual microphone</a></h3>

<p>The soundfield may be decoded to stereo using a pair of virtual microphones.<div class='note'><span class='notelabel'>NOTE:</span> A matrix type decoder, see <a href="./../Classes/FoaDecoderMatrix.html#*newStereo">FoaDecoderMatrix: *newStereo</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// virtual microphone stereo decoder
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderMatrix.newStereo


// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;// audio signal
    var angle, azim;            // angle and azimuth control


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top     = push to plane wave
    //            bottom  = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);


    // ------------------------------------------------------------
    // decode (to stereo)
    FoaDecode.ar(sig, ~decoder);

}.scope;
)

// ------------------------------------------------------------</textarea>
<h3><a class='anchor' name='Ambisonic%20UHJ'>Ambisonic UHJ</a></h3>

<p>Ambisonic UHJ stereo<a class='footnote anchor' name='footnote_org_1' href='#footnote_1'><sup>1</sup></a>  is the <em>native</em> stereo format for Ambisonics. A B-format signal (2D, with some losses) can be recovered from a UHJ decoded signal through the use of <a href="./../Classes/FoaEncoderKernel.html#*newUHJ">FoaEncoderKernel: *newUHJ</a>.<div class='note'><span class='notelabel'>NOTE:</span> A kernel type decoder, see <a href="./../Classes/FoaDecoderKernel.html#*newUHJ">FoaDecoderKernel: *newUHJ</a> for further details.</div><div class='warning'><span class='warninglabel'>WARNING:</span> Kernel decoders require special care. Allow the kernel time to load before attempting to use. Additionally, the kernel buffer should be freed through the use of <a href="./../Classes/FoaDecoderKernel.html#-free">FoaDecoderKernel: -free</a> after use.</div><textarea class='editor'>// ------------------------------------------------------------
// UHJ (stereo) decoder
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderKernel.newUHJ    // kernel decoders should be freed after use!!
                                    // free below...

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;// audio signal
    var angle, azim;            // angle and azimuth control


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top         = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);


    // ------------------------------------------------------------
    // decode (to stereo)
    FoaDecode.ar(sig, ~decoder);

}.scope;
)

// free kernel
~decoder.free

// ------------------------------------------------------------</textarea>
<h2><a class='anchor' name='Binaural'>Binaural</a></h2>
<h3><a class='anchor' name='Synthetic'>Synthetic</a></h3>

<p>The Ambisonic Tookit provides a synthetic spherical head model HRTF decoder.<a class='footnote anchor' name='footnote_org_2' href='#footnote_2'><sup>2</sup></a>  Ten subjects with varying head sizes are available. Audition to find one that works best for you.
<p>Additionally, HRTF decoders computed from measured HRIRs are also available: <a href="./../Classes/FoaDecoderKernel.html#*newListen">FoaDecoderKernel: *newListen</a> &amp; <a href="./../Classes/FoaDecoderKernel.html#*newCIPIC">FoaDecoderKernel: *newCIPIC</a>.<div class='note'><span class='notelabel'>NOTE:</span> A kernel type decoder, see <a href="./../Classes/FoaDecoderKernel.html#*newSpherical">FoaDecoderKernel: *newSpherical</a> for further details.</div><div class='warning'><span class='warninglabel'>WARNING:</span> Kernel decoders require special care. Allow the kernel time to load before attempting to use. Additionally, the kernel buffer should be freed through the use of <a href="./../Classes/FoaDecoderKernel.html#-free">FoaDecoderKernel: -free</a> after use.</div><textarea class='editor'>// ------------------------------------------------------------
// Binaural (synthetic) decoder
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderKernel.newSpherical // kernel decoders should be freed after use!!
                                         // free below...

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;// audio signal
    var angle, azim;            // angle and azimuth control


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top    = push to plane wave
    //            bottom = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);


    // ------------------------------------------------------------
    // decode (to binaural)
    FoaDecode.ar(sig, ~decoder);

}.scope;
)

// free kernel
~decoder.free

// ------------------------------------------------------------</textarea>
<h3><a class='anchor' name='CIPIC'>CIPIC</a></h3>

<p>Measured HRTF decoder, with measurements from the University of California Davis' <a href="http://interface.cipic.ucdavis.edu/sound/hrtf.html">CIPIC HRTF database</a>.<a class='footnote anchor' name='footnote_org_3' href='#footnote_3'><sup>3</sup></a>  Forty-five subjects with varying head sizes are available. Audition to find one that works best for you.<div class='note'><span class='notelabel'>NOTE:</span> A kernel type decoder, see <a href="./../Classes/FoaDecoderKernel.html#*newCIPIC">FoaDecoderKernel: *newCIPIC</a> for further details.</div><div class='warning'><span class='warninglabel'>WARNING:</span> Kernel decoders require special care. Allow the kernel time to load before attempting to use. Additionally, the kernel buffer should be freed through the use of <a href="./../Classes/FoaDecoderKernel.html#-free">FoaDecoderKernel: -free</a> after use.</div><textarea class='editor'>// ------------------------------------------------------------
// Binaural (CIPIC) decoder
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderKernel.newCIPIC    // kernel decoders should be freed after use!!
                                        // free below...

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;// audio signal
    var angle, azim;            // angle and azimuth control


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top         = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);


    // ------------------------------------------------------------
    // decode (to binaural)
    FoaDecode.ar(sig, ~decoder);

}.scope;
)

// free kernel
~decoder.free

// ------------------------------------------------------------</textarea>
<h2><a class='anchor' name='2D%20surround'>2D surround</a></h2>
<h3><a class='anchor' name='Quadraphonic'>Quadraphonic</a></h3>

<p>The Ambisonic Toolkit provides an optimised quadraphonic decoder with variable loudspeaker angle. The below example uses the default settings, which results in a square layout, <code>'single'</code> band type ( <code>'energy'</code> ) decoder. This sort of decoder is suitable for mid-scale playback, though, for best results for an audience, the use of a larger array (5+ loudspeakers) is advised. <a href="./../Classes/FoaDecoderMatrix.html#*newPanto">FoaDecoderMatrix: *newPanto</a> or <a href="./../Classes/FoaDecoderMatrix.html#*newDiametric">FoaDecoderMatrix: *newDiametric</a> would be appropriate.
<p>A psychoacoustically optimised (dual-band) near-field compensated decoder, suitable for studio monitoring, is demonstrated <a href="#Optimised%20quadraphonic">below</a>.<div class='note'><span class='notelabel'>NOTE:</span> A matrix type decoder, see <a href="./../Classes/FoaDecoderMatrix.html#*newQuad">FoaDecoderMatrix: *newQuad</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// quad decoder
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderMatrix.newQuad

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;                // audio signal
    var angle, azim;            // angle and azimuth control
    var fl, bl, br, fr;            // quad output channels


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top         = push to plane wave
    //          bottom    = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);

    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);



    // ------------------------------------------------------------
    // decode (to quad)
    #fl, bl, br, fr = FoaDecode.ar(sig, ~decoder);
    [fl, fr, bl, br]    // reorder output to match speaker arrangement

}.scope;
)
// ------------------------------------------------------------</textarea>
<h3><a class='anchor' name='DecodeB2'>DecodeB2</a></h3>

<p>By default, SuperCollider includes a pantophonic (2D) decoder, <a href="./../Classes/DecodeB2.html">DecodeB2</a>. This inbuilt decoder provides functionality similar to the Ambisonic Toolkit's <a href="./../Classes/FoaDecoderMatrix.html#*newPanto">FoaDecoderMatrix: *newPanto</a>, with the exceptions of a variable <strong>k</strong> argument and the documentation features of <a href="./../Classes/FoaDecoderMatrix.html">FoaDecoderMatrix</a>, e.g. <a href="./../Classes/FoaDecoderMatrix.html#-dirChannels">FoaDecoderMatrix: -dirChannels</a>.
<p>The inbuilt decoder is a <code>'controlled'</code> <strong>k</strong> decoder. (See <a href="./../Classes/FoaDecoderMatrix.html#Decoder%20k">this discussion</a> on <strong>k</strong>.) The below code includes a function, <code>funK</code>, to add variable <strong>k</strong> functionality to <a href="./../Classes/DecodeB2.html">DecodeB2</a>. So, this example is realised as a <code>'single'</code> band type ( <code>'energy'</code> ) decoder, matching the <a href="./../Classes/FoaDecoderMatrix.html#*newQuad">FoaDecoderMatrix: *newQuad</a> example <a href="#Quadraphonic">above</a>.<div class='note'><span class='notelabel'>NOTE:</span> See <a href="./../Classes/DecodeB2.html#*ar">DecodeB2: *ar</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// compare to SuperCollider's inbuilt DecodeB2 (as quad decoder)
//
// mono pink noise source
// omni encoder


// define encoder matrix and decoder channels
~encoder = FoaEncoderMatrix.newOmni
~numChans = 4

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~numChans

// function to adjust k of DecodeB2.ar
(
var funK;

funK = { arg k;
    if ( k.isNumber, {
        k
    }, {
        switch(k,
            'velocity', { [1, 2, 2, 2] },
            'energy', { [1, 2.sqrt, 2.sqrt, 2.sqrt] },
            'controlled', { [1, 1, 1, 1] },
            'single', { [1, 2.sqrt, 2.sqrt, 2.sqrt] }
        )
    }
    )
};

~kScale = funK.value('single');         // specify ATK's default,
)                                       // a single band ('energy') decoder

(
{
    var sig;                            // audio signal
    var angle, azim;                    // angle and azimuth control
    var fl, bl, br, fr;                 // quad output channels
    var w, x, y, z;                     // b-format channels (split)


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format("inbuilt").postln;

    // angle ---&gt; top           = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //            centre        = centre
    //            hard right    = back
    azim = MouseX.kr(pi, -pi);

    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;                         // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);

    // ------------------------------------------------------------
    // split to w, x, y, z, and scale k
    #w, x, y, z = sig * ~kScale;

    // ------------------------------------------------------------
    // decode (to quad), and match gain to ATK decoders
    #fl, fr, br, bl = DecodeB2.ar(~numChans, w, x, y) * 6.neg.dbamp;
    [fl, fr, bl, br]    // reorder output to match speaker arrangement

}.scope;
)
// ------------------------------------------------------------</textarea>
<h3><a class='anchor' name='Optimised%20quadraphonic'>Optimised quadraphonic</a></h3>

<p>The decoder presented here is an example of a dual-band ( <code>'dual'</code> ) psychoacoustically optmisied, near-field compensated decoder described by Gerzon.<a class='footnote anchor' name='footnote_org_4' href='#footnote_4'><sup>4</sup></a>  This sort of decoder is considered the ideal for first order Ambisonics, meeting all the criteria outlined by Gerzon to qualify as Ambisonic, <a class='footnote anchor' name='footnote_org_5' href='#footnote_5'><sup>5</sup></a>  and is the choice for critical studio listening.
<p>Additionally, this decode is rendered as a 'narrow quadraphonic' layout, with loudspeaker angles at <code>[ 30.0, 150.0, -150.0, -30.0 ]</code>. For studio based work, this can be convenient, as the front pair is at the correct angle for two channel stereo monitoring. The narrow layout gives increased localisation and stabilised images at front and back, at the expense of reduced stability at the sides.
<p>Near-field compensation, filtering for the near-field effects of loudspeaker placement, is made through the use of <a href="./../Classes/FoaNFC.html">FoaNFC</a>.<div class='note'><span class='notelabel'>NOTE:</span> A matrix type decoder, see <a href="./../Classes/FoaDecoderMatrix.html#*newQuad">FoaDecoderMatrix: *newQuad</a> and <a href="./../Classes/FoaNFC.html">FoaNFC</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// narrow quad decoder, psychocacousticly optimised, &amp; with NFC
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderMatrix.newQuad(pi/6, 'dual')
~distance = 1.2                 // louspeaker distance, for NFC, in meters


// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg
~distance

(
{
    var sig;                            // audio signal
    var angle, azim;                    // angle and azimuth control
    var fl, bl, br, fr;                 // quad output channels


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;


    // angle ---&gt; top           = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //            centre        = centre
    //            hard right    = back
    azim = MouseX.kr(pi, -pi);

    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;                         // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);



    // ------------------------------------------------------------
    // nfc &amp; decode (to quad)
    sig = FoaTransform.ar(sig, 'nfc', ~distance);
    #fl, bl, br, fr = FoaDecode.ar(sig, ~decoder);
    [fl, fr, bl, br]    // reorder output to match speaker arrangement

}.scope;
)
// ------------------------------------------------------------</textarea>
<h3><a class='anchor' name='ITU%205.0'>ITU 5.0</a></h3>

<p>The Ambisonic Toolkit includes <a href="http://www.brucewiggins.co.uk/?page_id=78">Bruce Wiggins'</a> optimised ITU 5.0 decoders.<a class='footnote anchor' name='footnote_org_6' href='#footnote_6'><sup>6</sup></a> <div class='note'><span class='notelabel'>NOTE:</span> A matrix type decoder, see <a href="./../Classes/FoaDecoderMatrix.html#*new5_0">FoaDecoderMatrix: *new5_0</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// 5.0 decoder
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderMatrix.new5_0

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;// audio signal
    var angle, azim;            // angle and azimuth control
    var fc, fl, bl, br, fr;        // 5.0 output channels
    var lo;                 // low freq channel place holder


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top         = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);


    // ------------------------------------------------------------
    // decode (to 5.0)
    #fc, fl, bl, br, fr = FoaDecode.ar(sig, ~decoder);
    lo = Silent.ar;

    [fl, fr, fc, lo, bl, br]    // reorder output to match speaker arrangement

}.scope;
)
// ------------------------------------------------------------</textarea>
<h2><a class='anchor' name='3D%20surround'>3D surround</a></h2>
<h3><a class='anchor' name='Cube'>Cube</a></h3>

<p>A full 3D decoder, with eight loudspeakers arranged in upper and lower rings of four. This small eight channel array is not optimal for large scale playback. For public performance, a 10 or 12 channel arrangement (two rings of 5 or 6) is more suitable.
<p>The loudspeaker layout specified by this decoder is more suited to a small-scale situation. See <a href="#Optimised%20bi-rectangle">below</a> for a minimal arrangement appropriate for full 3D studio monitoring.<div class='note'><span class='notelabel'>NOTE:</span> A matrix type decoder, see <a href="./../Classes/FoaDecoderMatrix.html#*newPeri">FoaDecoderMatrix: *newPeri</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// periphonic (3D) decoder (8-channels arranged as a cube)
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderMatrix.newPeri

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg

(
{
    var sig;                // audio signal
    var angle, azim;            // angle and azimuth control
    var flu, blu, bru, fru;        // cube output channels
    var fld, bld, brd, frd;


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top         = push to plane wave
    //          bottom    = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);

    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);



    // ------------------------------------------------------------
    // decode (to cube)
    #flu, blu, bru, fru, fld, bld, brd, frd = FoaDecode.ar(sig, ~decoder);
    [flu, fru, blu, bru, fld, frd, bld, brd]    // reorder output to match speaker arrangement

}.scope;
)
// ------------------------------------------------------------</textarea>
<h3><a class='anchor' name='Optimised%20bi-rectangle'>Optimised bi-rectangle</a></h3>

<p>This bi-rectangular decoder has been described by Gerzon as optimal for small-scale, full 3D listening. The decoder presented is an example of a dual-band ( <code>'dual'</code> ) psychoacoustically optmisied, near-field compensated decoder. Meeting all the criteria outlined by Gerzon to qualify as Ambisonic, this decoder is a good choice for full 3D critical studio listening.
<p>The frontal loudspeaker pair is arranged at <code>[ 30.0, -30.0 ]</code> degrees. For studio based work, this can be convenient, as the front pair is at the correct angle for two channel stereo monitoring.
<p>Near-field compensation, filtering for the near-field effects of loudspeaker placement, is made through the use of <a href="./../Classes/FoaNFC.html">FoaNFC</a>.<div class='note'><span class='notelabel'>NOTE:</span> A matrix type decoder, see <a href="./../Classes/FoaDecoderMatrix.html#*newDiametric">FoaDecoderMatrix: *newDiametric</a> and <a href="./../Classes/FoaNFC.html">FoaNFC</a> for further details.</div><textarea class='editor'>// ------------------------------------------------------------
// diametric 3d decoder (8-channels in a bi-rectangle)
//             psychocacousticly optimised, &amp; with NFC
//
// mono pink noise source
// omni encoder


// define encoder / decoder matrices
~encoder = FoaEncoderMatrix.newOmni
~decoder = FoaDecoderMatrix.newDiametric(
    [[30, 0], [-30, 0], [90, 35.3], [-90, 35.3]].degrad,
    'dual'
)
~distance = 1.2                 // louspeaker distance, for NFC, in meters

// inspect
~encoder.kind
~encoder.numChannels
~encoder.dirChannels

~decoder.kind
~decoder.numChannels
~decoder.dirChannels.raddeg
~distance

(
{
    var sig;                // audio signal
    var angle, azim;            // angle and azimuth control
    var fl, fr, bl, br;            // bi-rectangle output channels
    var slu, sru, sld, srd;


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top         = push to plane wave
    //          bottom    = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);

    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar;             // mono pink noise


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, 'push', angle, azim);



    // ------------------------------------------------------------
    // nfc &amp; decode (to bi-rectangle)
    sig = FoaTransform.ar(sig, 'nfc', ~distance);
    #fl, fr, slu, sru, br, bl, srd, sld = FoaDecode.ar(sig, ~decoder);
    [fl, fr, bl, br, slu, sru, sld, srd]    // reorder output to match speaker arrangement

}.scope;
)
// ------------------------------------------------------------</textarea>

<p><div class='footnotes'>
<a class='anchor' name='footnote_1'/><div class='footnote'>[<a href='#footnote_org_1'>1</a>] - See: <a href="http://en.wikipedia.org/wiki/Ambisonic_UHJ_format">http://en.wikipedia.org/wiki/Ambisonic_UHJ_format</a></div><a class='anchor' name='footnote_2'/><div class='footnote'>[<a href='#footnote_org_2'>2</a>] - See: R. O. Duda, "Modeling head related transfer functions," in Proceedings of the Twenty-Seventh Annual Asilomar Conference on Signals, Systems and Computers, Asilomar, CA, 1993.</div><a class='anchor' name='footnote_3'/><div class='footnote'>[<a href='#footnote_org_3'>3</a>] - See: <a href="http://interface.cipic.ucdavis.edu/sound/hrtf.html">http://interface.cipic.ucdavis.edu/sound/hrtf.html</a></div><a class='anchor' name='footnote_4'/><div class='footnote'>[<a href='#footnote_org_4'>4</a>] - M. A. Gerzon, "Multi-system ambisonic decoder," Wireless World, pp. 43-47, 69-73, July/Aug. 1977.</div><a class='anchor' name='footnote_5'/><div class='footnote'>[<a href='#footnote_org_5'>5</a>] - E. Benjamin, R. Lee, and A. Heller, "Is My Decoder Ambisonic?," in Proceedings of the 125th Audio Engineering Society Convention, San Francisco, 2008.</div><a class='anchor' name='footnote_6'/><div class='footnote'>[<a href='#footnote_org_6'>6</a>] - Irregular decoders in the Ambisonic Toolkit are kindly provided by Bruce Wiggins: <a href="http://www.brucewiggins.co.uk/">http://www.brucewiggins.co.uk/</a>. See also: B. Wiggins, I. Paterson-Stephens, V. Lowndes, and S. Berry, "The design and optimisation of surround sound decoders using heuristic methods," in Proceedings of UKSIM 2003: Conference on Computer Simulation, Cambridge, England, 2003.</div></div><div class='doclink'>helpfile source: <a href='file:///home/stefan/.local/share/SuperCollider/downloaded-quarks/atk-sc3/HelpSource/Guides/Decoding-FOA.schelp'>/home/stefan/.local/share/SuperCollider/downloaded-quarks/atk-sc3/HelpSource/Guides/Decoding-FOA.schelp</a><br>link::Guides/Decoding-FOA::<br></div></div><script src='./../editor.js' type='text/javascript'></script>
</body></html>